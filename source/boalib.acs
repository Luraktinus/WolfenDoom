#library "boalib"
#include "zcommon.acs"
#import "stealth.acs" //mxd - Please include the WHOLE folder structure while compiling this on recent Slade builds (ozy81)

#define SUPPLY_CHEST_PROXY_SCRIPT 4000
#define MAX_VOLUME 127
#define MAX_OBJECTIVES 6
#define MAX_PLAYER_HEALTH 150

global int 1:disable_mutants; //mxd. When set to true, Mutant and MutantMelee will immediately despawn after spawning.
global int 2:disable_big_mutants; //mxd. When set to true, BigMutant1 and BigMutant2 will immediately despawn after spawning.
global int 3:disable_supermutants; //mxd. When set to true, UberMutant and SuperMutant will immediately despawn after spawning.
global int 4:astrostein; //[Ed} When set to true, grenades will change to the Astrostein variant
global int 5:HealthScriptDeactivate;
global int 6:mission_number;		//The variable for the mission number

int objectives_on[8];			//Objective display toggle

int objectives_active = 0;
str objectives[MAX_OBJECTIVES][2]=
{
	{"",""},
	{"",""},
	{"",""},
	{"",""},
	{"",""},
	{"",""}
};

script "boaobjectives" (void)
{
	If(
		(GetLevelInfo(LEVELINFO_LEVELNUM)==99)
	)
	Terminate;

	if(objectives_on[PlayerNumber()])
		objectives_on[PlayerNumber()]=0;
	else
		objectives_on[PlayerNumber()]=1;
}

/* Change an objective
 * mode - 0 for icon, 1 for text.
 * objnumber - 0-indexed objective number. 0-2 are primary, 3-5 are secondary.
 * objtext - Objective text. MO_ICON_OPN or MO_ICON_ACC if mode is 0, LANGUAGE entry name if mode is 1.
 */
script "boaobjectivesset"(int mode,int objnumber,int objtext)
{
	if (mode == 1) { // Objective text
		// No curly braces needed here, since there's just one statement after the if/else.
		if (StrLen(StrParam(l:objtext)) > 0)
			objectives_active |= (1 << objnumber);
		else
			// Blank objective text
			objectives_active &= ~(1 << objnumber);
	}
	objectives[objnumber][mode]=objtext;
}

script "boaobjectivesdisplay" ENTER
{
	sethudsize(800,600,1);
	while(!objectives_on[PlayerNumber()])
		delay(1);
		setfont("SMALLFONT");

	int intMsgID = 960;

	int intHeaderCount = 0;

	for (int i = 0; i <= 7; i++)
	{
		SetFont("SMALLFONT");
		int intPosY = 135.0 + (13.0 * (intMsgID - 960)) + 0.1;

		if (i == 0)
		{
			HudMessage(l:"MO_PRIMARY"; HUDMSG_PLAIN, intMsgID++, CR_UNTRANSLATED, 300.1, intPosY, 0.03);
			intHeaderCount++;
		}
		else if (i == 4)
		{
			HudMessage(l:"MO_SECONDARY"; HUDMSG_PLAIN, intMsgID++, CR_UNTRANSLATED, 300.1, intPosY, 0.03);
			intHeaderCount++;
		}
		else
		{
			int objIdx = i - intHeaderCount;
			str strOutput = StrParam(l:objectives[objIdx][0], l:objectives[objIdx][1]);

			If (
				strLen(strOutput) > 0 &&
				((objectives_active >> objIdx) & 1) == 1)
			{
				HudMessage(s:strOutput; HUDMSG_PLAIN, intMsgID++, CR_UNTRANSLATED, 310.1, intPosY, 0.03);

				If (!StrCmp(StrParam(l:objectives[objIdx][0]), StrParam(l:"MO_ICON_ACC")))
					SetFont("OBJ_YES");
				Else
					SetFont("OBJ_NO");
				HudMessage(s:"A"; HUDMSG_PLAIN, intMsgID - 10, CR_UNTRANSLATED, 300.1, intPosY + 1.0, 0.03);
			}
		}
	}

	setfont("OBJECTGX");
	hudmessage(s:"A";
		HUDMSG_PLAIN,972,CR_UNTRANSLATED,400.0,115.1,0.03);
	delay(1);
	restart;
}

script "boaobjectiveaccomplished" (void)
{
	AmbientSound("misc/objective_acc", 57);
	sethudsize(800,600,1);
	setfont("OBJICON");
	hudmessage(s:"A";
		HUDMSG_FADEINOUT,990,CR_UNTRANSLATED,400.0,115.1,1.0,0.5,0.5);
	setfont("SMALLFONT");
	hudmessage(l:"MO_ACCOMP";
		HUDMSG_FADEINOUT,989,CR_UNTRANSLATED,400.0,130.1,1.0,0.5,0.5);

}

script "boaobjectiveadded" (void)
{
	AmbientSound("misc/objective_add", 57);
	sethudsize(800,600,1);
	setfont("OBJADD");
	hudmessage(s:"A";
		HUDMSG_FADEINOUT,990,CR_UNTRANSLATED,400.0,115.1,1.0,0.5,0.5);
	setfont("SMALLFONT");
	hudmessage(l:"MO_ADDED";
		HUDMSG_FADEINOUT,989,CR_UNTRANSLATED,400.0,130.1,1.0,0.5,0.5);

}

script "MissionNumber" OPEN
{
	if(GetLevelInfo(LEVELINFO_LEVELNUM)<99)					//If it's not a secret mission or the intermap
		mission_number=GetLevelInfo(LEVELINFO_LEVELNUM);	//Set the mission number just in case we warped/started ahead
}

script "BoADialogue"(int headtalky,int headsilent,int message)
{
	// Fade in HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 4, CR_UNTRANSLATED, 160.0, 24.0, 999.0, 1.0, 1.0);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 1, CR_UNTRANSLATED, 160.0, 24.0, 999.0, 1.0, 1.0); //mxd

	// Fade in the portrait
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 3, CR_UNTRANSLATED, 21.0, 16.0, 999.0, 1.0, 1.0);

	// Start talk animation
	Delay(35);
	SetFont(headtalky);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0);

	// Show message
	AmbientSound("RADIONOS",127);
	SetHudSize(640, 400, 1);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(l:message; HUDMSG_TYPEON | HUDMSG_LOG, 2, CR_GRAY, 100.1, 8.1, 999.0, 0.03, 1.0);

	// Stop talk animation
	delay(5*35);
	SetHudSize(320, 200, true);
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0);

	delay(5*35);

	// Fade out the portrait
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0, 0.0, 1.0);

	//Fade out HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 4, CR_UNTRANSLATED, 160.0, 24.0, 0.0, 0.0, 1.0);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 1, CR_UNTRANSLATED, 160.0, 24.0, 0.0, 0.0, 1.0); //mxd

	//Fade out the message
	SetHudSize(640, 400, 1);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(l:message; HUDMSG_FADEINOUT, 2, CR_GRAY, 100.1, 8.1, 0.0, 0.0, 1.0);
}

script "TotaleMacht" (void)
{
	delay(35 * 30); // 30 seconds
	TakeInventory("BerserkToken", 1);
}

//sprinting stamina
int cooldown[8];

script "Dashing" ENTER
{
	bool checksprint = GetCVar("sprintswitch");

	if(!CheckActorClass(0,"BoAPlayer"))
		terminate;
	int buttons = GetPlayerInput(-1,INPUT_BUTTONS);

	// Unfortunately, we have to check for each type of disguise token here - can't check for the base DisguiseToken class in ACS
	if (
		!(
			CheckWeapon("NullWeapon") &&
			(
				CheckInventory("SSBJUniformToken") ||
				CheckInventory("ScientistUniformToken")
			)
		) && 
		(buttons & BT_SPEED) && 
		(buttons & BT_FORWARD) && 
		!(buttons & BT_CROUCH) && 
		CheckInventory("Stamina") && 
		cooldown[PlayerNumber()] <= 0
	)
	{
		SetActorProperty(0,APROP_SPEED,2.0);

		if(checksprint==TRUE||CheckInventory("BerserkToken"))
			TakeInventory("Stamina",0); //do not deplete stamina - ozy
		else
		{
			TakeInventory("Stamina",random(1,3)); //more stamina to use
		}
	}
	else
	{
		SetActorProperty(0,APROP_SPEED,1.0);

		cooldown[PlayerNumber()]--;
		delay(random(1,3)); //recovery is more fast
		GiveInventory("Stamina",1);
	}

	if(!CheckInventory("Stamina"))
		cooldown[PlayerNumber()]=100;

	delay(1);
	restart;
}

Script "Exhausted" ENTER
{
	if(!CheckActorClass(0,"BoAPlayer")||GameType()==GAME_TITLE_MAP||CheckWeapon("NullWeapon"))
		terminate;
	while(CheckInventory("Stamina")<25)
	{
		if(GetActorProperty(0,APROP_WATERLEVEL)<3)
		PlaySound(0,"player/breathing",CHAN_VOICE);
		Delay(50);
	}
	Delay(1);
	restart;
}

//mxd. Mutants despawn checks
script "DisableMutants" (void)
{
	if(disable_mutants) Thing_Remove(0);
}

script "DisableBigMutants" (void)
{
	if(disable_big_mutants) Thing_Remove(0);
}

script "DisableSuperMutants" (void)
{
	if(disable_supermutants) Thing_Remove(0);
}

//Astrostein check
script "AstrosteinCheck" (void)
{
	SetResultValue(astrostein);
}

//Compass
bool compassActive[8];

int dots;		//Items to appear on compass
int marks[50];	//Items with tids

//FUNCTIONS
function int _cos(int a)
{
	a%=1.0;
	if(a<0)
		a+=1.0;
	if(a>0.5)
		a=-a+1.0;
	return cos(a);
}

function int _sin(int a)
{
	return _cos(a+0.25);
}

script "BoA_Compass" ENTER
{
	if(compassActive[playerNumber()])
	{
		SetHUDSize(1024,768,1);
		int a=GetActorAngle(0);
		int x=-_cos(a)*34/1.0,y=_sin(a)*34/1.0;
		SetFont("COMPASS1");
		HUDMessage(s:"N"; HUDMSG_PLAIN,-2,CR_UNTRANSLATED,x * 1.0 + 85.0,y * 1.0 + 170.0,0.03);
		HUDMessage(s:"S"; HUDMSG_PLAIN,-3,CR_UNTRANSLATED,-x * 1.0 + 85.0,-y * 1.0 + 170.0,0.03);
		HUDMessage(s:"E"; HUDMSG_PLAIN,-4,CR_UNTRANSLATED,-y * 1.0 + 85.0,x * 1.0 + 170.0,0.03);
		HUDMessage(s:"W"; HUDMSG_PLAIN,-5,CR_UNTRANSLATED,y * 1.0 + 85.0,-x * 1.0 + 170.0,0.03);
		SetFont("COMPASS");
		HUDMessage(s:"A"; HUDMSG_PLAIN | HUDMSG_NOTWITHFULLMAP,-1,CR_UNTRANSLATED,85.0,162.0,0.03);
		for(int i=0;i<dots;i++)
			drawdot(4000+i);
		for(i=0;marks[i]!=0;i++)
			drawdot(marks[i]);
	}
	Delay(1);
	restart;
}

script "BoA_CompassQueue"(void)
{
	if(ActivatorTID())
	{
		int i;
		while(marks[i]!=0)
			i++;
		marks[i]=ActivatorTID();
	}
	else
		Thing_ChangeTID(0,4000+dots++);
}

function void drawdot(int tid)
{
	SetHUDSize(1024,768,1);
	if(GetActorProperty(tid,APROP_Health)>0)
	{
		int dx = GetActorX(0) - GetActorX(tid),dy = GetActorY(0) - GetActorY(tid);
		int a = GetActorAngle(0) - VectorAngle(dx, dy) - 0.25;
		dx /= 1.0;
		dy /= 1.0;
		int dist = sqrt(dx*dx + dy*dy),rdist = dist / 10;
		if(rdist > 33)
			rdist = 33;

		if(CheckActorClass(tid,"ObjectiveIcon"))
		{
			SetFont("GOAL3");
			HUDMessage(s:"A"; HUDMSG_ALPHA, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03,0.2);
		}
		else if(GetActorProperty(tid,APROP_RenderStyle)!=STYLE_None)
		{
			SetFont("GOAL2");
			HUDMessage(s:"A"; HUDMSG_ALPHA, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03,0.2);
		}

		if(CheckActorClass(tid,"Akten")||CheckActorClass(tid,"AktenEisenmann")||CheckActorClass(tid,"SpearOfDestiny")||CheckActorClass(tid,"ExclamationCompass"))
		{
			SetFont("GOAL1");
			HUDMessage(s:"A"; HUDMSG_ALPHA, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03,0.2);
		}
		else if(GetActorProperty(tid,APROP_RenderStyle)!=STYLE_None)
		{
			SetFont("GOAL2");
			HUDMessage(s:"A"; HUDMSG_ALPHA, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03,0.2);
		}

		/*SetFont("SmallFont");
		HUDMessage(i:dist / 32, s:" m"; HUDMSG_PLAIN, 0, CR_RED, 85.0, 240.1, 0.03);*/
	}
}

//SCRIPTS
script "BoA_Objectives"(void) //Call this script to activate and draw the compass
{
	if(compassActive[playerNumber()])
		compassActive[playerNumber()]=0;
	else
		compassActive [playerNumber()]=1;
}

script "UnderwaterBubbles" ENTER
{
	if(GetActorProperty(0,APROP_WATERLEVEL)>=3 && GetCVar("bubbleswitch")) //check also stored cvar for custom tweaks
		SpawnForced("PlayerBubble",GetActorX(0)+random(4,8),GetActorY(0),GetActorZ(0)+random(48,52),0,random(0,255));
	delay(7);
	restart;
}

//Teleportscript from Re-Exhumed
script "ExTeleportPlayer" (int teleportID, int destinationID)
{
	int deltaX = GetActorX(playerID) - GetActorX(teleportID);
	int deltaY = GetActorY(playerID) - GetActorY(teleportID);
	//mxd. Warp won't work here, because the activator is ExTeleportIfPlayerBelow or ExTeleportIfPlayerAbove and not a player.
	//mxd. This kinda limits Warp usefulness, dont you think?..
	SetActorPosition(playerID, GetActorX(destinationID) + deltaX, GetActorY(destinationID) + deltaY, GetActorZ(playerID), false);
}

script "ExGetPlayerZ" (void)
{
	SetResultValue(GetActorZ(playerID) >> 16);
}

// A proxy for the SupplyChest script that can be called from a thing's special.
// Called by a supply chest when a player tries to open it. The chest is the
// activator and the chest's target is the player.
script SUPPLY_CHEST_PROXY_SCRIPT (int prize, int script_number, int objective)
{
	ACS_NamedExecuteAlways("SupplyChest", 0, prize, script_number, objective);
}

//Supply Chest Script
Script "SupplyChest"(int prize, int script_number, int objective_number)
{
	int supplychestid = ActivatorTID();

	// Make the player the activator.
	if(SetActivatorToTarget(0) && CheckInventory("ChestKey"))
	{
		TakeInventory("ChestKey",1);
		ActivatorSound("misc/p_pkup",127);

		// Predefined rewards.
		str reward = "EmptySupplyChest";
		switch (prize)
		{
		case 1: reward = "SupplyChest100Coins"; break;
		case 2: reward = "SupplyChest75Coins"; break;
		case 3: reward = "SupplyChest25Coins"; break;
		case 4: reward = "SupplyChestKar98k"; break;
		case 5: reward = "SupplyChestFieldKits"; break;
		case 6: reward = "SupplyChestGrenades"; break;
		default:
			// No reward.
			break;
		}

		int x = GetActorX(supplychestid);
		int y = GetActorY(supplychestid);
		int z = GetActorZ(supplychestid);

		// Remove the old chest (to prevent multiple uses).
		Thing_Remove(supplychestid);

		// Spawn reward with same TID as chest.
		SpawnForced(reward, x, y, z, supplychestid);

		// Call a custom script if specified.
		if (script_number)
		{
			ACS_ExecuteAlways(script_number, 0, supplychestid);
		}

		// Update objectives if specified.
		if (objective_number >= 0 && objective_number < MAX_OBJECTIVES)
		{
			ACS_NamedExecuteAlways("boaobjectivesset",0,0,objective_number,"MO_ICON_ACC");
			ACS_NamedExecuteAlways("boaobjectiveaccomplished",0,0,0,0);
		}
	}
	else
	{
		ActivatorSound("treasure/locked",127);
	}
}

//Clear Inventory - now added Astrostein stuff (Ozy81)
script "CLEAR INVENTORY"(void)
{
	TakeInventory("KnifeSilent", 9999); //All ammo and weapons (aside from the knife) should now be handled by my EventHandler - Ed
	TakeInventory("BoABlueKey",1);
	TakeInventory("BoAYellowKey",1);
	TakeInventory("BoARedKey",1);
	TakeInventory("BoACyanKey",1);
	TakeInventory("BoAGreenKey",1);
	TakeInventory("BoAPurpleKey",1);
	TakeInventory("AstroBlueKey",1); //Astros
	TakeInventory("AstroYellowKey",1); //Astros
	TakeInventory("AstroRedKey",1); //Astros
	TakeInventory("Akten",1);
	TakeInventory("AktenV2",1);
	TakeInventory("CutsceneEnabled", 1);
}

script "NOGRENADES" (void)
{
	TakeInventory("GrenadePickup", 9999);
	TakeInventory("AstroGrenadePickup", 9999);
}
/*
  SetMusicVolumeFactor
	Sets the multiplier for music volume (128 is half volume, 256 is full volume,
	512 is double volume, etc.), over the delay specified in tics.

	Example:
	    ACS_NamedExecuteAlways("SetMusicVolumeFactor", 0, 128, 70);

	This will fade the music to half volume over 2 seconds (70 tics).
*/
int intSetVolume = 1.0;
Script "SetMusicVolumeFactor" (int intTargetVolume, int intDelay)
{
	intTargetVolume *= 256;

	If (intDelay > 0)
	{
		int intStepSize = (intSetVolume - intTargetVolume) / intDelay;

		For (int i = 0; i <= intDelay; i++)
		{
			SetMusicVolume(intSetVolume - (intStepSize * i));
			Delay(1);
		}
	}

	SetMusicVolume(intTargetVolume);
	intSetVolume = intTargetVolume;
}

Script "QuickKick" (void)
{
	int timeout;

	If (
		//(CheckInventory("Health") < 30) ||
		(CheckInventory("Stamina") < 30) ||
		(GetSpeed(0) > 30) ||
		(GetActorViewHeight(0) >> 16 < 56) ||
		(GetLevelInfo(LEVELINFO_LEVELNUM)==99) //let's remove the kick while on HQs - ozy81
	)
		Terminate; //No kicking unless you have stamina, aren't walking, and aren't crouching

	str strPrevWeapon = GetWeapon(); //Assign previous weapon name to a variable - gets "None" if there is none

	SetPlayerProperty(0, 1, PROP_INSTANTWEAPONSWITCH); //Enable fast weapon switch

	While (!SetWeapon("BJKickAuto")) //Give the player the auto-kick
	{
		GiveInventory("BJKickAuto", 1);
		Delay(1);
	}

	Delay(25); //Wait for the animation and attack

	If (StrICmp(strPrevWeapon, "None")) //Reset the old weapon
	{
		While (!SetWeapon(strPrevWeapon) && timeout < 35) // Wait until the weapon is set, or a second has elapsed
		{
			Delay(5);
			timeout += 5;
		}
	}

	TakeInventory("BJKickAuto", 1); //Remove the auto-kick from inventory

	SetPlayerProperty(0, 0, PROP_INSTANTWEAPONSWITCH); //Disable fast weapon switch
}

Function int GetSpeed(int tid)
{
	int x = GetActorVelX(tid);
	int y = GetActorVelY(tid);
	int z = GetActorVelZ(tid);

	int speed = (FixedMul(x, x) + FixedMul(y, y) + FixedMul(z, z)) >> 16;

	return speed;
}

//fadescript for map setup
script "Maplaunch" ENTER
{
	FadeRange(0,0,0,1.0,0,0,0,0.0,1.5);
}

//underwater sounds
script "Underwater" ENTER
{
	if(GetActorProperty(0,APROP_WATERLEVEL)>=3)
	{
		delay(3);
		localambientsound("underwtr",random(90,127));
	}
	delay(35*8);	//only 1 sound is 8sec long, 2 4sec & 1 2.5sec
	restart;
}

//death script
script "OhNoes!" DEATH
{
	SetHudSize(320, 240, true);
	LocalSetMusic("THEEND");

	SetFont("fade");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1000, CR_UNTRANSLATED, 160.0, 120.0, 999999.0, 6.0, 1.0);

	// [ZK] If you resurrect, the death screen should disappear immediately
	for (int i = 0; i < 35*6; i++)
	{
		Delay(1);

		if (GetActorProperty(0, APROP_HEALTH) > 0)
		{
			SetFont("fade");
			HudMessage(s:""; HUDMSG_PLAIN, -1000, CR_UNTRANSLATED, 0.0, 0.0, 0.0, 0.0);
			LocalSetMusic("*");
			terminate;
		}
	}

	SetFont("youdied");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1001, CR_UNTRANSLATED, 160.0, 120.0, 999999.0, 3.0, 1.0);

	// [ZK] Delay 1 tic instead of 35
	while(GetActorProperty(0,APROP_HEALTH) <= 0) Delay(1);

	//fadeback if resurrected
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1001, CR_UNTRANSLATED, 160.0, 120.0, 0.0, 0.0, 0.25);
	SetFont("fade");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1000, CR_UNTRANSLATED, 160.0, 120.0, 0.0, 0.0, 1.0);

	// [ZK] restore music
	LocalSetMusic("*");
}

//lowhealth scripts
script "30Health" ENTER
{
	int width = 640, height = 480;
	SetHudSize(width, height, 0);
	SetFont("M_INJ");

	// Half width and height (in fixed-point).
	int half_width  = (width / 2) << 16;
	int half_height = (height / 2) << 16;

	while (true)
	{
		int health = GetActorProperty(0, APROP_HEALTH);
		if (!HealthScriptDeactivate && health > 0 && health <= 30)
		{
			HudMessage(s:"A"; HUDMSG_FADEINOUT, 0, CR_WHITE, half_width, half_height, 0.0, 0.5, 0.5);

			// Volume and delay of heartbeat (faster and louder with less health).
			int volume = MAX_VOLUME;
			int delay1 = 10;
			int delay2 = 14;

			if (health > 20)
			{
				volume = MAX_VOLUME - 50;
				delay1 = 21;
				delay2 = 34;
			}
			else if (health > 10)
			{
				volume = MAX_VOLUME - 25;
				delay1 = 18;
				delay2 = 24;
			}

			LocalAmbientSound("hbeat", volume);
			Delay(delay1);
			LocalAmbientSound("hbeat", volume);
			Delay(delay2);
		}

		Delay(1);
	}
}

script "IcyScreen" ENTER
{
	int width = 320, height = 240;
	SetHudSize(width, height, 0);
	SetFont("M_ICED");

	// Half width and height (in fixed-point).
	int half_width  = (width / 2) << 16;
	int half_height = (height / 2) << 16;

	while (true)
	{
		int cold = GetActorPowerupTics(0, "PowerSlow");
		if (!HealthScriptDeactivate && cold)
		{
			HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_LAYER_UNDERHUD | HUDMSG_NOTWITHFULLMAP | HUDMSG_NOTWITHOVERLAYMAP | HUDMSG_ALPHA , 0, CR_UNTRANSLATED, half_width, half_height, 0.0, 0.5, cold, 0.2);
		}
		
		Delay(1);
	}
}

script "AdrenalineScreen" ENTER
{
	int width = 346, height = 256;
	SetHudSize(width, height, 0);
	SetFont("M_INJ");

	// Half width and height (in fixed-point).
	int half_width  = (width / 2) << 16;
	int half_height = (height / 2) << 16;

	while (true)
	{
		int drugs = GetActorPowerupTics(0, "PowerDrugs");
		int factor = GetActorPowerupTics(0, "PowerFactor");
		if (factor && drugs)
		{
			HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_LAYER_UNDERHUD | HUDMSG_NOTWITHFULLMAP | HUDMSG_NOTWITHOVERLAYMAP , 0, CR_UNTRANSLATED, half_width, half_height, 0.0, 0.5, drugs);
		}
		
		Delay(1);
	}
}

// Vitality upgrade script, modified from Trailblazer. Thanks, PillowBlaster!
script "VSerum Max Health Boost" (void)
{
	if (GetActorProperty(0, APROP_SpawnHealth) < MAX_PLAYER_HEALTH) {
		SetActorProperty(0, APROP_SpawnHealth, GetActorProperty(0, APROP_SpawnHealth) + 5);
	}
	GiveInventory("Meal", 5);
}

script "PlayerHealthAbove25" (void)
{
	SetResultValue(GetActorProperty(playerID, APROP_Health) >= 25);
}

// Level tilt handling

int leveltiltpitch, leveltiltangle;

// This is the main loop that controls the player view roll and pitch alterations
// It also controls the velocity changes that make the player slide "downhill"
script "leveltilt" ENTER
{
	int True_Pitch = GetActorPitch(0);

	while (1)
	{
		// The handling of changing the player's roll has been moved into the main Tilt++ code in ZScript.
		// Only pitch adjusments are being made here!
		int angle = GetActorAngle(0) - leveltiltangle / 360;

		True_Pitch -= GetPlayerInput(-1, INPUT_PITCH);

		int fixedpitch = FixedDiv(leveltiltpitch, 360.0);

		SetActorPitch(0, True_Pitch + FixedMul(cos(angle), fixedpitch));

		SetUserVariable(0, "leveltilt", leveltiltpitch);
		SetUserVariable(0, "leveltiltangle", leveltiltangle);

		Delay(1);
	}
}

// This is the wrapper script that lets you set the level tilt smoothly
//
//   pitch 		integer - Desired tilt amount in degrees from vertical (so 0 is no tilt)
//   angle		integer - Desired map angle to tilt along (0 is rotation around y axis, 90 is rotation around x axis)
//   speed		fixed point - Controls the speed of interpolation (defaults to 5.0)
//   override	bool - Set to non-zero to force the tilt amount to change immediately instead of being interpolated
//
// The tilt change amount will start out small, and increase as the tilt angle increases
//
script "ChangeTilt" (int pitch, int angle, int speed, int override)
{
	if (override) { leveltiltpitch = pitch * 1.0; leveltiltangle = angle * 1.0; Terminate; }

	if (!speed) { speed = 5.0; }
	int targetpitch = pitch * 1.0;
	int targetangle = angle * 1.0;
	int mintiltchange = max(FixedMul(FixedDiv(abs(leveltiltpitch), 180.0), speed), 0.001); // Base max tilt change step amount on starting tilt angle

	int tiltspeed;

	if (targetpitch > 180.0) { targetpitch -= 360.0; }
	else if (targetpitch < -180.0) { targetpitch += 360.0; }

	while (
		leveltiltpitch - tiltspeed > targetpitch ||
		leveltiltpitch + tiltspeed < targetpitch ||
		leveltiltangle - 1.0 > targetangle ||
		leveltiltangle + 1.0 < targetangle
	)
	{
		if (leveltiltpitch > 180.0) { leveltiltpitch -= 360.0; }
		else if (leveltiltpitch < -180.0) { leveltiltpitch += 360.0; }

		if (leveltiltangle > 180.0) { leveltiltangle -= 360.0; }
		else if (leveltiltangle < -180.0) { leveltiltangle += 360.0; }

		tiltspeed = max(FixedMul(FixedDiv(abs(leveltiltpitch), 180.0), speed), mintiltchange); // Tilt faster as you get higher in angle

		if (leveltiltpitch - tiltspeed > targetpitch) { leveltiltpitch -= tiltspeed; }
		else if (leveltiltpitch + tiltspeed < targetpitch) { leveltiltpitch += tiltspeed; }

		if (leveltiltangle - 1.0 > targetangle) { leveltiltangle -= 1.0; }
		else if (leveltiltangle + 1.0 < targetangle) { leveltiltangle += 1.0; }

		Delay(1);
	}

	leveltiltpitch = targetpitch;
	leveltiltangle = targetangle;
}

// Alternate script to forceably set the tilt amount
script "SetTilt" (int pitch, int angle)
{
	ACS_NamedExecute("ChangeTilt", 0, pitch, angle, 999999999);
}

// Math helper functions
function int abs (int x)
{
	if (x < 0)
		return -x;

	return x;
}

function int pow (int x, int n)
{
	int y = 1;
	while (n-- > 0) y *= x;
	return y;
}

function int min (int a, int b)
{
	if (a < b)
		return a;

	return b;
}

function int max (int a, int b)
{
	if (a > b)
		return a;

	return b;
}