/*

  Flatsprite floor/ceiling vent that is 0-height but walkable and solid.

  Most suitable for use as a breakable ceiling/floor vent, but can also have its
  pitch changed to match sloped surfaces, though the hitbox and blocking do not
  change, so the effect may not work quite as desired.

*/
class Debris_Vent_FloorCeiling : SwitchableDecoration
{
	bool broken;

	Default
	{
		//$Category Special Effects (Wolf3D)
		//$Title Vent Debris - Floor/Ceiling (activatable, solid/walkable)
		//$Color 12

		+SOLID
		+NOGRAVITY
		+ACTLIKEBRIDGE
		+USESPECIAL
		+FLATSPRITE
		+SHOOTABLE
		+DONTTHRUST
		+NOBLOOD
		+NOTAUTOAIMED
		Health 1;
		Height 0;
		Radius 32;
		Activation THINGSPEC_Switch | THINGSPEC_ThingTargets;
	}

	States
	{
		Spawn:
			VENT A 1;
		Inactive:
		Death:
		Active:
			VENT "#" 0 {
				if (!broken && target && abs(pos.z - target.pos.z) < 48) { return ResolveState("Unblock"); }
				return ResolveState(null);
			}
		StaticLoop:
			VENT "#" 35;
			Wait;
		Unblock:
			VENT B 0 {
				broken = True;

				A_SpawnItemEx("PowerPlantSmokePuffSmall", 0, 0, 32, 0, 0, 0, 0, 0);

				S_Sound("DSMETDST", CHAN_AUTO, 0.5, ATTN_NORM);

				for (int i = 0; i < 4; i++) {
					A_SpawnItemEx("Debris_Trash", 0, random(-32,32), random(0,64), random(-3,3), random(0,2), random(-1,4), 0, SXF_CLIENTSIDE);
					A_SpawnItemEx("Debris_Trash2", 0, random(-32,32), random(0,64), random(-3,3), random(0,2), random(-1,4), 0, SXF_CLIENTSIDE);
				}

				bNoInteraction = True;
			}
			VENT B -1;
			Stop;
	}

}
/*
class Debris_Wood_FloorCeiling : Debris_Vent_FloorCeiling
{
	Default
	{
		//$Title Wood Debris - Floor/Ceiling (activatable, solid/walkable)
	}

	States
	{
		Unblock:
			VENT B 0 {
				broken = True;

				A_SpawnItemEx("PowerPlantSmokePuffSmall", 0, 0, 32, 0, 0, 0, 0, 0); //needed? - Ozy81

				S_Sound("WOODBRK", CHAN_AUTO, 0.5, ATTN_NORM);

				for (int i = 0; i < 4; i++) {
					A_SpawnItemEx("Debris_Wood", 0, random(-32,32), random(0,64), random(-3,3), random(0,2), random(-1,4), 0, SXF_CLIENTSIDE);
					A_SpawnItemEx("Debris_Wood", 0, random(-32,32), random(0,64), random(-3,3), random(0,2), random(-1,4), 0, SXF_CLIENTSIDE);
				}

				bNoInteraction = True;
			}
			VENT B -1;
			Stop;
	}

}*/