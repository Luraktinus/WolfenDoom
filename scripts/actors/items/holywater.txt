/*
 * Holy water grenade
 * Damages all occult enemies within its radius
 */

class HolyWaterEffect : Inventory {
	Default {
		Inventory.MaxAmount 1;
	}
}

class HolyWaterGrenade : CustomInventory {
	
	int EffectRadius;
	int HealthFactor;
	class<Inventory> EffectItem;
	Color selfLightColor;
	Color enemyLightColor;
	PointLightPulse effectLight;

	property EffectRadius: EffectRadius;
	property InverseHealthFactor: HealthFactor;
	property EffectItem: EffectItem;
	property SelfLightColor: SelfLightColor;
	property EnemyLightColor: EnemyLightColor;

	Default {
		HolyWaterGrenade.EffectRadius 512;
		HolyWaterGrenade.InverseHealthFactor 2;
		HolyWaterGrenade.EffectItem "HolyWaterEffect";
		HolyWaterGrenade.SelfLightColor "#66c1ff";
		HolyWaterGrenade.EnemyLightColor "#66c1ff";
		Inventory.Icon "I_HOLWGR";
		Tag "$TAGHOLYW";
		Speed 60;
		+INVENTORY.INVBAR;
	}
	States {
		Spawn:
			HOLG A 1 NoDelay {
				invoker.effectLight = PointLightPulse(Spawn("PointLightPulse", pos));
				invoker.effectLight.args[0] = invoker.selfLightColor.r;
				invoker.effectLight.args[1] = invoker.selfLightColor.g;
				invoker.effectLight.args[2] = invoker.selfLightColor.b;
				invoker.effectLight.args[3] = 16;
				invoker.effectLight.args[4] = 12;
				invoker.effectLight.Angle = 70.0;
			}
			HOLG A -1 {
				A_Gravity();
				A_RadiusHolyEffect();
			}
			Stop;
		Pickup:
			HOLG A 0 {
				invoker.effectLight.Destroy();
			}
			Stop;
		Use:
			TNT1 A 0 A_FireCustomMissile("HolyWaterGrenade");
			Stop;
	}

	action void A_RadiusHolyEffect() {
		Actor enemy = invoker.RoughMonsterSearch(invoker.EffectRadius);
		if (enemy) {
			if (invoker.ShouldAffect(enemy)) {
				if (invoker.ShouldDamage(enemy)) {
					enemy.health = enemy.health / invoker.HealthFactor;
					PointLightPulse enemyEffectLight = PointLightPulse(Spawn("PointLightPulse", enemy.pos));
					enemyEffectLight.args[0] = invoker.EnemyLightColor.r;
					enemyEffectLight.args[1] = invoker.EnemyLightColor.g;
					enemyEffectLight.args[2] = invoker.EnemyLightColor.b;
					enemyEffectLight.args[3] = enemy.radius * 2;
					enemyEffectLight.args[4] = enemy.radius;
					enemyEffectLight.Angle = 70.0;
					enemyEffectLight.target = enemy; // Makes light follow enemy
					enemy.GiveInventoryType(invoker.EffectItem);
				}
				invoker.effectLight.Args[3] = invoker.EffectRadius;
				invoker.effectLight.Args[4] = invoker.EffectRadius / 2;
			}
		}
	}
	
	/*
	 * Checks whether the actor is affected by holy water
	 * 
	 * By default, this affects non-boss occult monsters.
	 * @param actor The actor to check
	 */
	bool ShouldAffect (Actor actor) {
		if (actor.bBoss) return false;
		if (actor is "BloodSkull" ||
			actor is "WereWaffenSS" ||
			actor is "Geist" ||
			actor is "UndeadMonk") {
			return true;
		}
		return false;
	}
	
	/*
	 * Checks whether the holy water grenade should damage the actor.
	 * 
	 * By default, this checks whether the target actor has the EffectItem
	 * in their inventory.
	 * @param actor The actor to check
	 */
	bool ShouldDamage (Actor actor) {
		if (actor.CheckInventory(EffectItem, 1)) {
			return false;
		}
		return true;
	}
}