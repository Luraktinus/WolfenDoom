////////
//MAIN//
////////

//mxd. The only difference are footstep sounds...
ACTOR NaziBoss : Nazi
{
	Mass 1000
	Scale 0.67
	Base.BossIcon "BOSSICON"
	DamageFactor "Rocket", 0.2
	+BOSS
	States
	{
	See:
		Goto See.Boss
	}
}

//ozy - The difference here comes with quakes & frame sequences
ACTOR MechBoss : Nazi
{
	Mass 1000
	Scale 1.0
	Damagefactor "Normal", 0.75
	States
	{
	See:
	Idle:
		Goto See.BossMech
	}
}

//COUNTERS
ACTOR AwakeCounter : Inventory {Inventory.MaxAmount 1}
ACTOR ChainCounter : Inventory {Inventory.MaxAmount 48}
ACTOR CrouchCounter : Inventory {Inventory.MaxAmount 1}
ACTOR DodgeCounter : Inventory {Inventory.MaxAmount 6}
ACTOR EliteAssaulterCounter : Inventory {Inventory.MaxAmount 32}
ACTOR EliteGrenadeCounter : Inventory {Inventory.MaxAmount 4}
ACTOR ElsaCounter : Inventory {Inventory.MaxAmount 64}
ACTOR FlamerCounter : Inventory {Inventory.MaxAmount 50}
ACTOR MP40GuardCounter : Inventory {Inventory.MaxAmount 32}
ACTOR PistolGuardCounter : Inventory {Inventory.MaxAmount 8}
ACTOR PriestCounter : Inventory {Inventory.MaxAmount 12}
ACTOR RifleGuardCounter : Inventory {Inventory.MaxAmount 10}
ACTOR RocketCounter : Inventory {Inventory.MaxAmount 16}
ACTOR SSMP40GuardCounter : Inventory {Inventory.MaxAmount 32}
ACTOR SniperCounter : Inventory {Inventory.MaxAmount 10}
ACTOR UberCounter : Inventory {Inventory.MaxAmount 80}
ACTOR WMP40GuardCounter : Inventory {Inventory.MaxAmount 32}
ACTOR WaffenSSCounter : Inventory {Inventory.MaxAmount 40}

//ATTACKS - NAZIS
ACTOR ClusterBomb
{
	Radius 4
	Height 3
	Speed 40
	Damage 1
	DamageType "Frag"
	Reactiontime 8 //for countdown
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	Obituary "$OBGRENADE"
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.6
	SeeSound "clusterbomb/fire"
	BounceSound "grenade/bounce"
	States
	{
	Spawn:
		TRGN A 20 A_SetAngle(angle + 359, SPF_INTERPOLATE)
		"####" A 0 A_Countdown
		Loop
	Death:
		TRGN A 35
		TNT1 A 0 A_AlertMonsters
		"####" A 0 A_SetScale(1.75,1.75)
		"####" A 0 A_SetTranslucent(0.75,1)
		"####" A 0 A_PlaySound("clusterbomb/explode", CHAN_AUTO, 1.0, FALSE, 0.1)
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator")
		"####" A 0 A_SpawnItemEx("KD_HL2SparkGenerator")
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnitemEx("ClusterBomb_Debris", 0, 0, 8, random(2,16), random(2,16), random(2,16), random(0,359), 0, 0)
		"####" A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

ACTOR ClusterBomb_Debris
{
	Radius 8
	Height 16
	Mass 20
	Damage 2
	Scale 0.5
	Gravity 0.75
	Projectile
	-NOGRAVITY
	+RANDOMIZE
	+ROLLSPRITE
	+SOLID
	BounceType "Hexen"
	BounceFactor 0.8
	BounceSound "clusterbomb/debrisbounce"
	WallBounceFactor 0.8
	BounceCount 5
	States
	{
	Spawn:
		NLPJ A 4 NoDelay NoDelay {
							A_SetRoll(roll + 15, SPF_INTERPOLATE);
							A_JumpIf(waterlevel == 3, "AdjustMass");
							}
		Loop
	AdjustMass: //mxd
		"####" "#" 0 A_SetMass(400)
		Goto Swim
	Swim: //mxd
		"####" A 2 A_ScaleVelocity(0.7)
		"####" A 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	Death:
		"####" A 0
		Stop
	}
}

ACTOR HardClusterBomb_Debris : ClusterBomb_Debris { +MTHRUSPECIES Damage 3}

ACTOR GasBomb : ClusterBomb
{
	Speed 30
	DamageType "MutantPoison"
	Reactiontime 12
	Obituary "$OBGASBOMB"
	DeathSound "world/barrelboom"
	Translation "16:47=[34,0,7]:[0,0,0]","168:191=[36,0,9]:[2,0,0]"
	States
	{
	Spawn:
		TRGN A 25 A_SetAngle(angle + 359, SPF_INTERPOLATE)
		"####" A 0 A_Countdown
		Loop
	Death:
		TRGN A 40
		TNT1 A 0 A_AlertMonsters
		"####" A 0 A_Scream
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator")
		"####" A 0 A_SpawnItemEx("KD_HL2SparkGenerator")
		"####" A 0 BRIGHT A_SpawnProjectile("PoisonCloudUndead",16,0,0,2,0)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("UndeadFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("UndeadSmoke",random(-64,64),random(-64,64),random(-48,48),momx,0,frandom(0.5,1),0,SXF_TRANSFERTRANSLATION,160)
		"####" A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

ACTOR NebRocketEnemy : NebRocket
{
	Speed 15
	-FORCERADIUSDMG
	SeeSound "nebelwerfer/fire"
	States
	{
	Death:
		FRME A 1 BRIGHT LIGHT(NEBEXPLO) A_Explode(random(25,30))
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(NEBEXPLO)

		Stop
	}
}

ACTOR HardNebRocketEnemy : NebRocketEnemy { +MTHRUSPECIES }

ACTOR MiniRocket
{
	Radius 11
	Height 8
	Speed 15
	Damage 5
	Projectile
	+RANDOMIZE
	Scale 0.47
	SeeSound "nazi/missilef"
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		NMIS A 0 NoDelay A_PlaySound("nazi/missileengine", CHAN_AUTO, 0.3, TRUE)
		NMIS A 1 BRIGHT LIGHT(OTTOFIRE) A_SpawnItemEx("RocketFlame",random(-1,1),0,random(-1,1))
		Wait
	Death:
		FRME A 1 BRIGHT LIGHT(OTTOFIRE) A_Explode(20,40)
		FRME A 0 A_StopSound(CHAN_AUTO)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(OTTOFIRE)
		Stop
	}
}

ACTOR HardMiniRocket : MiniRocket { +MTHRUSPECIES }

ACTOR MiniMiniRocket : MiniRocket
{
	Scale 0.27
	Damage (random(1,4)*4)
	DeathSound "nebelwerfer/xplode"
}

ACTOR HardMiniMiniRocket : MiniMiniRocket { +MTHRUSPECIES }

ACTOR SeekerRocket : MiniRocket
{
	Scale 0.27
	Damage (random(1,4)*4)
	MaxTargetRange 128
	+SEEKERMISSILE
	DeathSound "nebelwerfer/xplode"
	States
	{
	Spawn:
		NMIS A 0 NoDelay A_PlaySound("nazi/missileengine", CHAN_AUTO, 0.3, TRUE)
		NMIS A 1 BRIGHT LIGHT(OTTOFIRE) {A_SeekerMissile(8, random(8,16)); A_SpawnItemEx("RocketFlame",random(-1,1),0,random(-1,1));}
		Wait
	Death:
		FRME A 1 BRIGHT LIGHT(OTTOFIRE) A_Explode(20,40)
		FRME A 0 A_StopSound(CHAN_AUTO)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(OTTOFIRE)
		Stop
	}
}

ACTOR HardSeekerRocket : SeekerRocket { +MTHRUSPECIES MaxTargetRange 192 }

//ATTACKS - ROBOTS
ACTOR MechaRocket : MiniRocket
{
	Speed 27
	States
	{
	Death:
		FRME A 1 BRIGHT LIGHT(OTTOFIRE) A_Explode(25,45) //5 dmg more
		FRME A 0 A_StopSound(CHAN_AUTO)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(OTTOFIRE)
		Stop
	}
}

ACTOR HardMechaRocket : MechaRocket { +MTHRUSPECIES }

ACTOR MiniMechaRocket : MiniRocket
{
	Speed 27
	Scale 0.27
	Damage (random(1,4)*5) //20 dmg max vs 16
	DeathSound "nebelwerfer/xplode"
}

ACTOR HardMiniMechaRocket : MiniMechaRocket { +MTHRUSPECIES }

//ATTACKS - BOSSES
ACTOR FlyingNeedle : MiniRocket
{
	Speed 10
	DamageType "MutantPoison"
	PoisonDamage 3, 4
	SeeSound ""
	DeathSound "gshardnd"
	Gravity 0.3
	Scale 0.47
	-NOGRAVITY
	+ADDITIVEPOISONDAMAGE
	+ADDITIVEPOISONDURATION
	States
	{
	Spawn:
		NEDL ABCD 3
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR HardFlyingNeedle : FlyingNeedle { +MTHRUSPECIES }

ACTOR FlyingHack : FlyingNeedle
{
	Speed 13
	Scale 0.27
	DamageType "None"
	PoisonDamage 1, 2
	+NOGRAVITY
	+ROLLSPRITE
	States
	{
	Spawn:
		COPR A 0 NODELAY A_SetRoll(random(0, 360), 0, 0)
		Rotate:
		COPR A 1 A_SetRoll(roll+20, SPF_INTERPOLATE, 0)
		Loop
	Death:
		TNT1 A 1 A_SpawnItemEx("HackPuff")
		Stop
	}
}

ACTOR HardFlyingHack : Flyinghack { +MTHRUSPECIES Speed 18 }

ACTOR HackPuff
{
	+NOBLOCKMAP
	+NOEXTREMEDEATH
	+NOGRAVITY
	+PUFFONACTORS
	RenderStyle "Add"
	States
	{
	Spawn:
	Melee:
	Crash:
		POOF A 3 A_PlaySound("knife/stone")
		POOF BCDE 3
		Stop
	Death:
	XDeath:
		POOF A 3 A_PlaySound("knife/hit")
		Goto Crash+1
	}
}

ACTOR Flamerbolt : Flamebolt
{
	Damage (random(0,1))
	States
	{
	Animation:
		"####" A 3 BRIGHT LIGHT(BOAFLAMW)
			{
				A_SetScale(ScaleX+0.05);
				A_FadeOut(0.05);
			}
		"####" A 0 A_Jump(128, "Smoke1", "Smoke2", "Smoke3")
		Goto Death
	Death:
		"####" A 1 LIGHT(BOAFLAMW)
			{
				A_SetScale(ScaleX+0.05);
				A_FadeOut(0.05);
			}
		Goto Death
	}
}

ACTOR EnemyFlamerShot : Flamerbolt { -RIPPER }
ACTOR HardEnemyFlamerShot : EnemyFlamerShot { +MTHRUSPECIES }
ACTOR EnemyFlamebolt : Flameball { Damage 8 }
ACTOR HardEnemyFlamebolt : EnemyFlamebolt { +MTHRUSPECIES }

ACTOR EnemyFlamebolt2 : EnemyFlamebolt
{
	Damage (random(1,4)*5)
	Scale .35
	States
	{
	Death:
		FBLX ABCDEFGHIJK 2 BRIGHT LIGHT(BOAFLMW2)
		Stop
	}
}

ACTOR HardEnemyFlamebolt2 : EnemyFlamebolt2 { +MTHRUSPECIES }

//ATTACKS - MUTANTS
ACTOR MutantFartCloud
{
	Game Doom
	Radius 2
	Height 2
	DamageType "MutantPoison"
	+NOINTERACTION
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.3
	States
	{
	Spawn:
		FUGS AAAAA 2
	Fade:
		FUGS AAAAA 2 A_FadeOut (0.05)
		FUGS AAAAA 2 A_FadeIn (0.05)
		FUGS AAAAA 2 A_FadeOut (0.05)
		FUGS AAAAA 2 A_FadeIn (0.05)
		FUGS AAAAA 2 A_FadeOut (0.05)
		FUGS AAAAA 2 A_FadeIn (0.05)
		FUGS AAAAA 2 A_FadeOut (0.05)
		FUGS AAAAA 2 A_FadeIn (0.05)
		Stop
	}
}

ACTOR MutantSmoke
{
	Game Doom
	Radius 2
	Height 2
	Scale 0.3
	Gravity 0.3
	Alpha 0.5
	VSpeed 2.5
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+MOVEWITHSECTOR
	+NOBLOCKMAP
	+NOGRAVITY
	+WINDTHRUST
	RenderStyle "Shaded"
	StencilColor "E0 00 E0"
	States
	{
	Spawn:
		TNT1 A 0
		CLFX A 8 A_FadeOut(0.05)
		Wait
	}
}

ACTOR MutantRocket
{
	Radius 8
	Height 8
	Speed 12
	FastSpeed 14
	Damage (random(2,6))
	DamageType "MutantPoison"
	Projectile
	Scale 0.4
	+RANDOMIZE
	SeeSound "nazi/missilef"
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		NMIS A 0 NoDelay A_PlaySound("nazi/missileengine", CHAN_AUTO, 0.3, TRUE)
	Fly:
		NMIS A 1 BRIGHT LIGHT(MUTNROCK) A_SpawnItemEx("MutantFlame",random(-1,1),0,random(-1,1))
		Loop
	Death:
		FUGS A 1 BRIGHT LIGHT(MUTNROCK) A_Explode(10,30)
		FUGS A 0 A_StopSound(CHAN_AUTO)
		FUGS A 0 BRIGHT A_SpawnProjectile("PoisonCloudMutant",16,0,0,2,0)
		FUGS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("MutantFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
		FUGS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("MutantSmoke",random(-64,64),random(-64,64),random(-48,48),momx,0,frandom(0.5,1),0,SXF_TRANSFERTRANSLATION,160)
		FUGS AAAAAAAAAAAAA 1 BRIGHT LIGHT(MUTNROCK)
		Stop
	}
}

ACTOR HardMutantRocket : MutantRocket { +MTHRUSPECIES }

ACTOR PoisonCloudMutant
{
	Radius 20
	Height 30
	Reactiontime 5
	+BLOCKEDBYSOLIDACTORS
	+BLOODLESSIMPACT
	+CANBLAST
	+DONTSPLASH
	+DONTTHRUST
	+DROPOFF
	+FOILINVUL
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NOGRAVITY
	RenderStyle "Translucent"
	Alpha 0.6
	DamageType "MutantPoison"
	States
	{
	Spawn:
		TNT1 A 1
		"####" A 2 A_Explode(5,128,0,0,128)
		"####" A 20 A_Countdown
		Loop
	Death:
		"####" A 7
		"####" A 6
		Stop
	}
}

ACTOR MutantBounceBall
{
	Radius 4
	Height 8
	Scale 0.3
	Projectile
	Speed 12
	Damage 3
	Gravity 0.125
	RenderStyle "Add"
	BounceType "Grenade"
	BounceFactor 0.9
	WallBounceFactor 0.9
	BounceCount 5
	DeathSound "supaproj/explode"
	BounceSound "supaproj/bounce"
	Obituary "$SUPABALL"
	-NOGRAVITY
	+THRUGHOST
	+WINDTHRUST
	States
	{
	Spawn:
		SMBL AB 4 A_SpawnItemEx("Spark_P1",0,0,0,random(1,2),random(1,2),random(1,2),random(1,360))
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_P1",0,0,0,random(1,2),random(1,2),random(1,2),random(1,360))
		TNT1 A 0 A_SpawnItemEx("SparkFlare_P1")
		SMBL CDE 4
		Stop
	}
}

ACTOR FlyingCleaver : MiniRocket
{
	Radius 8
	Speed 15
	Scale 0.45
	Damage (2*random(4,6))
	DamageType "None"
	SeeSound ""
	DeathSound "gshardnd"
	-RANDOMIZE
	+DONTFALL
	+NOGRAVITY
	+ROLLSPRITE
	+WALLSPRITE
	States
	{
	Spawn:
		MUPR A 0 NODELAY A_SetAngle(-90+angle, SPF_INTERPOLATE)
	Rotate:
		MUPR A 1 A_SetRoll(roll+20, SPF_INTERPOLATE, 0)
		Loop
	Death:
		MUPR A 1 { A_ChangeFlag("ROLLSPRITE", FALSE); A_SpawnItemEx("HackPuff"); }
		"####" A 1 A_FadeOut(0.1)
		Stop
	}
}

ACTOR HardFlyingCleaver : FlyingCleaver { +MTHRUSPECIES }

//OCCULT STUFF
ACTOR ZyklonBBomb : ClusterBomb
{
	Speed 20
    Gravity 0.3
	DamageType "UndeadPoison"
	Obituary "$OBZYKLON"
	DeathSound "mortard1"
	SeeSound "mortarf1"
	States
	{
	Spawn:
		ZBGN A 25 LIGHT(QUEENPLASMATRAIL_3) A_SetAngle(angle + 359, SPF_INTERPOLATE)
		"####" A 0 A_Countdown
		Loop
	Death:
		ZBGN A 40
		TNT1 A 0 A_AlertMonsters
		"####" A 0 A_Scream
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator")
		"####" A 0 A_Playsound("mortarg1")
		"####" A 0 LIGHT(QUEENPLASMATRAIL_1) A_SpawnProjectile("PoisonCloudUndead",16,0,0,2,0)
		"####" AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ZyklonBCloud",random(-16,16),random(-16,16),random(8,16),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(0.0,1.0),0,128)
		Stop
	}
}

ACTOR ZyklonBCloud
{
	Game Doom
	Radius 48
	Height 32
	Damage 2
	Scale 1.5
	PoisonDamage 4
	DamageType "UndeadPoison"
	Projectile
	+WINDTHRUST
	RenderStyle "Add"
	Alpha 0.4
	States
	{
	Spawn:
		ZBGS A 2
	Fade:
		"####" A 3 LIGHT(QUEENPLASMATRAIL_3) A_FadeOut (0.004)
		"####" A 0 A_SetScale(scaleX+0.01, scaleY+0.01)
		Loop
	}
}

ACTOR ZyklonBBall
{
	Radius 11
	Height 8
	Speed 15
	Damage 10
	PoisonDamage 2
	Scale 1.0
	DamageType "UndeadPoison"
	Projectile
	+BRIGHT
	+RANDOMIZE
	+SEEKERMISSILE
	Alpha 1.0
	RenderStyle "Add"
	SeeSound "tgfr1"
	DeathSound "tgfr2"
	States
	{
	Spawn:
		ZART AB 5 LIGHT(QUEENPLASMATRAIL_2) A_SeekerMissile(0, 15)
		Loop
	Death:
		"####" C 1 LIGHT(QUEENPLASMATRAIL_1)
		"####" CDEF 4 LIGHT(QUEENPLASMATRAIL_2)
		Stop
	}
}

ACTOR ZombieSoul
{
	+NOBLOCKMAP
	+NOGRAVITY
	Renderstyle "Add"
	Alpha 0.3
	Scale 0.5
	SeeSound "ZSCREAM2"
	States
	{
	Spawn:
		ZOFX ABCD 5 LIGHT(QUEENPLASMATRAIL_3)
		Stop
  }
}

ACTOR ZombieRocket : MiniRocket
{
	Speed 20
	Damage 3
	Renderstyle "Shadow"
	States
	{
	Spawn:
		NMIS A 0 NoDelay A_PlaySound("nazi/missileengine", CHAN_AUTO, 0.3, TRUE)
		NMIS A 1 BRIGHT LIGHT(OTTOFIRE) A_SpawnItemEx("ZombieFlame",random(-1,1),0,random(-1,1))
		Wait
	Death:
		FRME A 1 BRIGHT LIGHT(OTTOFIRE) A_Explode(15,35)
		"####" A 0 A_StopSound(CHAN_AUTO)
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator")
		"####" A 0 A_SpawnItemEx("KD_HL2SparkGenerator")
		"####" A 0 BRIGHT A_SpawnProjectile("PoisonCloudUndead",16,0,0,2,0)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("UndeadFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("UndeadSmoke",random(-64,64),random(-64,64),random(-48,48),momx,0,frandom(0.5,1),0,SXF_TRANSFERTRANSLATION,160)
		"####" BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(OTTOFIRE)
		Stop
	}
}

ACTOR HardZombieRocket : ZombieRocket { +MTHRUSPECIES }

ACTOR ZombieVomit
{
	Radius 8
	Height 8
	Speed 20
	FastSpeed 24
	Damage (random(3,8))
	Scale 0.7
	SeeSound "Blood/Spit"
	DeathSound "Blood/Impact"
	DamageType "UndeadPoison"
	Projectile
	Decal UndeadSmear
	States
	{
	Spawn:
		ZVOM A 0 A_SpawnItemEx("UndeadFlyingBloodTrail", 0, 0, 0, 1)
		ZVOM ABCDEF 1
		Loop
	Death:
		TNT1 A 0 BRIGHT A_SpawnProjectile("PoisonCloudUndead",16,0,0,2,0)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("UndeadSmoke",random(-64,64),random(-64,64),random(-48,48),momx,0,frandom(0.5,1),0,SXF_TRANSFERTRANSLATION,160)
		"####" AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ZyklonBCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
		"####" AAAAAAAAA 1 A_SpawnItemEx("UndeadFlyingBlood", random(2,-2), random(2,-2), random(2,2), random(1,6), random(1,6), random(1,6), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR BloodSkullCloud
{
	Game Doom
	Radius 48
	Height 32
	Scale 1.1
	Projectile
	+WINDTHRUST
	RenderStyle "Add"
	Alpha 0.4
	States
	{
	Spawn:
		ZBGS B 2
	Fade:
		"####" B 3 LIGHT(BSKULFOG) A_FadeOut(0.01)
		"####" B 0 A_SetScale(scaleX+0.01, scaleY+0.01)
		Loop
	}
}

ACTOR ZFlame1A : Flame1A
{
	States
	{
	Spawn:
		ZFLM A 0
		"####" A 0 ThrustThingZ(0,random(2,6),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" A 2 BRIGHT A_SetScale(ScaleX-0.0125, ScaleY-0.0125)
		"####" A 0 BRIGHT A_FadeOut(.05)
		Goto Spawn+3
	}
}

ACTOR ZFlame1B : ZFlame1A
{
	DistanceCheck "sfxlod"
	States
	{
	Spawn:
		ZFLM B 0
		Goto Super::Spawn+1
	}
}

ACTOR ZFlamebolt
{
	Speed 25
	Radius 16
	Damage (random(1,2))
	PoisonDamage 4
	Scale 0.1
	Alpha 0.9
	Projectile
	+BLOODLESSIMPACT
	+WINDTHRUST
	DamageType "UndeadPoison"
	RenderStyle "Add"
	Decal Scorch
	SeeSound "STEAM_BURST"
	Obituary "$OBZYKF"
	States
	{
	Spawn:
		"####" A 0 NODELAY A_Jump(256, "Flame1", "Flame2", "Flame3", "Flame4")
		Stop
	Flame1:
		ZFLM A 0
		Goto Animation
	Flame2:
		ZFLM B 0
		Goto Animation 
	Flame3:
		ZFLM C 0
		Goto Animation
	Flame4:
		ZFLM D 0
		Goto Animation
	Animation:
		"####" "#" 3 BRIGHT LIGHT(ZYKFLAMW)
			{
				A_SetScale(ScaleX+0.05);
				A_FadeOut(0.05);
			}
		"####" "#" 0 A_Jump(128, "Smoke1", "Smoke2", "Smoke3")
		Goto Death
	Smoke1:
		"####" "#" 0 A_SpawnItemEx("FlamerSmoke1")
		Goto Death
	Smoke2:
		"####" "#" 0 A_SpawnItemEx("FlamerSmoke2")
		Goto Death
	Smoke3:
		"####" "#" 0 A_SpawnItemEx("FlamerSmoke3")
		Goto Death
	Death:
		"####" "#" 1 LIGHT(ZYKFLAMW)
			{
				A_SetScale(ScaleX+0.05);
				A_FadeOut(0.05);
			}
		Goto Death
	}
}

ACTOR ZFlameball : ZFlamebolt
{
	Damage 10
	Speed 20
	Scale 0.3
	SeeSound "flamer/napalm"
	DeathSound "nebelwerfer/xplode"
	States
	{
	Spawn:
		ZBAL A 2 BRIGHT LIGHT(ZYKFLMW2)
			{
				A_SpawnItemEx("Spark_G1", random(-32,32), random(-32,32), random(-32,32), random(-2,2), random(-2,2), random(-2,2), random(0,359));
				A_SpawnItemEx("Spark_G1", random(-32,32), random(-32,32), random(-32,32), random(-2,2), random(-2,2), random(-2,2), random(0,359));
				A_SpawnItemEx("Spark_G1", random(-32,32), random(-32,32), random(-32,32), random(-2,2), random(-2,2), random(-2,2), random(0,359));
				if (waterlevel > 0) {
					A_ScaleVelocity(0.0);
					return State("Death");
				}
				return state(0);
			}
		"####" A 0 A_Jump(192, "Smoke1", "Smoke2", "Smoke3")
		Loop
	Smoke1:
		"####" A 0 A_SpawnItemEx("FlamerSmoke1")
		"####" A 0 A_Jump (192,1,2)
		"####" A 0 A_SpawnItemEx("ZFlame1A")
		"####" A 0 A_SpawnItemEx("ZFlame1B")
		Goto Spawn
	Smoke2:
		"####" A 0 A_SpawnItemEx("FlamerSmoke2")
		"####" A 0 A_Jump (192,1,2)
		"####" A 0 A_SpawnItemEx("ZFlame1A")
		"####" A 0 A_SpawnItemEx("ZFlame1B")
		Goto Spawn
	Smoke3:
		"####" A 0 A_SpawnItemEx("FlamerSmoke3")
		"####" A 0 A_Jump (192,1,2)
		"####" A 0 A_SpawnItemEx("ZFlame1A")
		"####" A 0 A_SpawnItemEx("ZFlame1B")
		Goto Spawn
	Death:
		"####" A 0 A_SpawnItemEx("NebSmokeFloor")
		"####" A 0 A_SpawnItemEx("NebSmokeMushroom",0,0,0)
		"####" A 0 A_SpawnItemEx("NebSmokePillar",0,0,0,0,0,2)
		"####" A 0 A_SetScale(1.0)
		"####" AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Spark_G1", random(-32,32), random(-32,32), random(-32,32), random(-4,4), random(-4,4), random(-4,4), random(0,359))
		"####" AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ZyklonBCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
		ZBLX A 2 BRIGHT LIGHT(ZYKFLMW2) A_Explode(random(32,64),128,0,FALSE,64,0,0)
		ZBLX BCDEFGHIJK 2 BRIGHT LIGHT(ZYKFLMW2)
		Stop
	}
}

ACTOR UndeadFartCloud
{
	Game Doom
	Radius 2
	Height 2
	DamageType "UndeadPoison"
	+NOINTERACTION
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.3
	States
	{
	Spawn:
		FUGZ ABCDE 2
	Fade:
		"####" ABCDE 2 A_FadeOut (0.05)
		"####" ABCDE 2 A_FadeIn (0.05)
		"####" ABCDE 2 A_FadeOut (0.05)
		"####" ABCDE 2 A_FadeIn (0.05)
		"####" ABCDE 2 A_FadeOut (0.05)
		"####" ABCDE 2 A_FadeIn (0.05)
		"####" ABCDE 2 A_FadeOut (0.05)
		"####" ABCDE 2 A_FadeIn (0.05)
		"####" ABCDE 2 A_FadeOut (0.05)
		Stop
	}
}

ACTOR UndeadSmoke : MutantSmoke { StencilColor "05 05 05" }

ACTOR PoisonCloudUndead
{
	Radius 20
	Height 30
	Reactiontime 2
	DamageType "UndeadPoison"
	RenderStyle "Translucent"
	Alpha 0.6
	+BLOCKEDBYSOLIDACTORS
	+BLOODLESSIMPACT
	+CANBLAST
	+DONTSPLASH
	+DONTTHRUST
	+DROPOFF
	+FOILINVUL
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 1
		"####" A 4 A_Explode(3,64,0,0,96)
		"####" A 20 A_Countdown
		Loop
	Death:
		"####" A 7
		"####" A 6
		Stop
	}
}

ACTOR QueenPlasmaBlast
{
	Radius 13
	Height 8
	Speed 25
	Damage 6
	Projectile
	DamageType "Disintegrate"
	Scale .75
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	+RANDOMIZE
	RenderStyle "Add"
	Alpha 0.75
	SeeSound "electricplasma/shoot"
	DeathSound "electricplasma/hit"
	Decal "SwordLightning"
	States
	{
	Spawn:
		EBLT GH 0 BRIGHT A_SpawnProjectile("QueenPlasmaBlastTrail", 0, 0, 0)
		"####" GH 2 BRIGHT A_BishopMissileWeave
		Loop
	Death:
		"####" IJK 3 bright
		Stop
  }
}

ACTOR QueenPlasmaBlastTrail
{
	Radius 13
	Height 8
	Speed 0
	Damage 0
	Projectile
	+RANDOMIZE
	RenderStyle "Add"
	Alpha 0.75
	States
	{
	Spawn:
		EBLT ABC 3 BRIGHT A_BishopMissileWeave
		Goto Death
  Death:
		"####" DEF 4 BRIGHT A_FadeOut(0.25)
		Loop
	}
}

ACTOR QueenPainPlasmaBlast : QueenPlasmaBlast {Speed 15}

ACTOR QueenBlastPod //From the Vore actor - Projectile sprites by Vader - Tweaked by Ozy81
{
	Radius 16
	Height 16
	Speed 6
	FastSpeed 9
	Damage (random(10,20))
	Projectile
	+BRIGHT
	+SEEKERMISSILE
	RenderStyle "Add"
	Alpha 0.9
	SeeSound "aode/fire"
	DeathSound "aode/hit"
	Decal BFGLightning
	Scale 2.5
	States
	{
	Spawn:
		CNOB A 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" A 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" A 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" A 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" B 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" B 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" B 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" B 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" C 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" C 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" C 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" C 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" C 0 A_JumpIfTargetInLOS(1, 360, 1)
		Loop
		"####" A 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" A 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" A 0 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" A 1 A_SpawnItemEx("QueenBlastPodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
		"####" C 0 A_JumpIfTargetInLOS(1, 360, 1)
		Goto Spawn+4
		"####" BBB 0 A_SeekerMissile(90, 90, 2)
		Goto Spawn+4
	Death:
		"####" D 0 A_Explode(30, 90)
		"####" DEFGHI 4
		Stop
	}
}

ACTOR QueenBlastPodTrail
{
	Radius 1
	Height 1
	Speed 0
	Projectile
	RenderStyle "Add"
	Alpha 0.9
	+NOCLIP
	States
	{
	Spawn:
	Death:
		TNT1 A 3
		CNOT ABCDE 3 BRIGHT
		Stop
	}
}

ACTOR SmokeMonsterProjectile
{
	Height 32
	Radius 16
	Speed 10
	Projectile
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 AAAAAA 0 A_SpawnItemEx("MonsterHole",0,0,8,random(-1,1),random(-1,1),random(-1,1),random(1,360))
		TNT1 A 1
		Loop
	}
}

ACTOR MonsterHole
{
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOTIMEFREEZE
	RenderStyle "Translucent"
	Alpha 0.3
	Scale .75
	States
	{
	Spawn:
		TNT1 A 2
		SMMO ABCDEFGHIJKLMNOPQR 2
		Stop
	}
}

ACTOR SkullBloodDrip
{
	Radius 1
	Height 4
	+DONTSPLASH
	+MISSILE
	Scale 0.4
	Gravity 0.7
	States
	{
	Spawn:
		BDR2 A 50
		Stop
	}
}

ACTOR BloodSpit
{
	Radius 6
	Height 6
	Speed 17
	Damage 3
	Scale 0.5
	SeeSound "Blood/Spit"
	DeathSound "Blood/Impact"
	Projectile
	Decal BloodSplat
	States
	{
	Spawn:
		BSHT AB 2 A_SpawnItemEx("BloodSpitTrail")
		Loop
	Death:
		BSHT CD 6
		Stop
	}
}

ACTOR BloodSpitTrail
{
	Radius 1
	Height 1
	Projectile
	RenderStyle "Translucent"
	Alpha 0.8
	Scale 0.3
	+NOCLIP
	States
	{
	Spawn:
		BSHT CD 4
		Stop
	}
}

//CRITTERS STUFF
//Following blockers are needed to block sharks, bats, spiders & scorpions
ACTOR SharkBlocker
{
	//$Category Monsters (Wolf3D)/Behavior
	//$Title Shark (blocker)
	//$Color 4
	Height 1
	Radius 256
	Mass 99999999
	Species "Player"
	+DONTSPLASH
	+GHOST
	+NOGRAVITY
	+SOLID
	+THRUSPECIES
}

//we need it smaller for narrow places/situations
ACTOR SharkBlockerMini : SharkBlocker
{
	//$Category Monsters (Wolf3D)/Behavior
	//$Title Shark (blocker, mini)
	//$Color 4
	Radius 32
}

ACTOR Gibs1
{
	Radius 10
	Height 10
	Mass 6
	Speed 14
	BounceType Doom
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13)
		GIB1 AB 0 A_Jump(256,"End")
		GIB2 ABC 0 A_Jump(256,"End")
		GIB3 BC 0 A_Jump(256,"End")
		GIB4 ABC 0 A_Jump(256,"End")
		GIB5 ABC 0 A_Jump(256,"End")
	End:
		"####" "#######" 4 A_SpawnProjectile("BloodFlySM",0,0,random(0,360),CMF_AIMDIRECTION|CMF_BADPITCH,random(-100,100))
		"####" "#" 700 A_SpawnProjectile("BloodFlySM",0,0,random(0,360),CMF_AIMDIRECTION|CMF_BADPITCH,random(-100,100))
		Stop
	}
}

ACTOR BloodTrail
{
	Radius 1
	Height 1
	Projectile
	+CLIENTSIDEONLY
	+NOCLIP
	States
	{
	Spawn:
		BLUD CBA 3
		Stop
	}
}

ACTOR BloodTrailSM : BloodTrail {Scale 0.5}

ACTOR BloodFly
{
	Radius 0
	Height 0
	Mass 50
	Speed 6
	Projectile
	-NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		BLUD C 2 A_SpawnItemEx("BloodTrail")
		Loop
	Death:
		BLUD CBA 8
		Stop
	}
}

ACTOR BloodFly2 : BloodFly
{
	Scale 0.6
	Speed 8
	States
	{
	Spawn:
		BLUD C 1
		Loop
	Death:
		BLUD CBA 4
		Stop
	}
}

ACTOR BloodFlySM : BloodFly
{
	Scale 0.5
	Mass 100
	Speed 8
	States
	{
	Spawn:
		BLUD C 1 A_SpawnItemEx("BloodTrailSM")
		Loop
	}
}

ACTOR ScorpionSpit
{
	Scale 0.5
	Radius 13
	Height 8
	Speed 28
	VSpeed 20
	Damage 6
	Projectile
	Gravity 0.4
	DamageType "PoisonCloud"
	PoisonDamage 20
	SeeSound "scorpion/spit"
	-NOGRAVITY
	States
	{
	Spawn:
		SPIT A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05,0.05),ScaleY + frandom(-0.05,0.05))
		Goto See
	See:
		SPIT A 3 A_SpawnItemEx("NashGore_FlyingBlood",0,0,0,0,0,0,0,128)
		Loop
	}
}

ACTOR ScorpionChunk
{
	Radius 8
	Height 8
	Scale 0.6
	Mass 50
	BounceType "Doom"
	BounceCount 1
	Gravity 0.6
	DeathSound "gore/splat"
	Projectile
	-NOGRAVITY
	+BOUNCEAUTOOFF
	+CANBOUNCEWATER
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MOVEWITHSECTOR
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1,0.1),ScaleY + frandom(-0.1,0.1))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7)
		SCRG ABCDEFG 0 A_Jump(256,"Chunk")
		Stop
	Chunk:
		SCRG "#" 6 A_SpawnItemEx("ScorpionFlyingBlood",random(-1,1),random(-1,1))
		Loop
	Death:
		SCRG "#" 0 A_SpawnItemEx("ScorpionBlood")
		SCRG "#" 250 A_CheckSight("Vanish")
		Wait
	Vanish:
		TNT1 A 0
		Stop
	}
}

ACTOR BigScorpionChunk : ScorpionChunk
{
	Scale 1.1
	Mass 60
	Gravity 0.8
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1,0.1),ScaleY + frandom(-0.1,0.1))
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7)
		SC2G ABCDEFG 0 A_Jump(256,"Chunk")
		Stop
	Chunk:
		SC2G "#" 6 A_SpawnItemEx("NashGore_FlyingBlood",random(-1,1),random(-1,1))
		Loop
	Death:
		SC2G "#" 0 A_SpawnItemEx("NashGore_Blood")
		SC2G "#" 250 A_CheckSight("Vanish")
		Wait
	}
}

//LOPER STUFF
ACTOR LoperZap
{
	+ALLOWPARTICLES
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	Mass 0
	RenderStyle "Translucent"
	Alpha 0.6
	Scale 0.5
	Translation "112:127=192:200"
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx("TPortLightningWaveSpawner2",0,0,8,0,0,0,0,0,160)
		EBLT HIG 3 BRIGHT A_SpawnItemEx("Spark_B1",0,0,8,0,0,1,0,0,160)
		Stop
	}
}

ACTOR ZTracer
{
	Radius 5
	Height 5
	Speed 15
	ReactionTime 175
	Damage 5
	DamageType "Fire"
	RenderStyle "Add"
	Alpha 0.67
	Projectile
	-NOGRAVITY
	+BRIGHT
	+FLOORHUGGER
	Translation "112:127=192:200"
	Obituary "$LOPRHIT3"
	SeeSound "electricplasma/shoot"
	DeathSound "electricplasma/hit"
	Decal "SwordLightning"
	States
	{
	Spawn:
	See:
		TNT1 A 1 LIGHT(LOPERLIT)
		"####" A 0 A_Countdown
		"####" A 0 A_SpawnProjectile("ZTracerPuff",0,0,0,0)
		Loop
	Death:
		EBLT G 4
		"####" H 4 A_Explode(16,16)
		"####" IJK 3
		Stop
	}
}

ACTOR HardZTracer : ZTracer { +MTHRUSPECIES }

ACTOR ZTracerPuff
{
	Radius 1
	Height 1
	Speed 0
	RenderStyle "Add"
	DamageType "Fire"
	Alpha 0.67
	Projectile
	-NOGRAVITY
	+FLOORHUGGER
	+RANDOMIZE
	Translation "112:127=192:200"
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx("TPortLightningWaveSpawner2",0,0,8,0,0,0,0,0,160)
		"####" AAAAAAAAA 0 A_SpawnItemEx("Spark_B1",0,0,8,0,0,1,0,0,160)
		EBLT ABCDEFEDCB 3 BRIGHT A_Explode(2,4)
		Stop
	}
}

//Support Actors from TPortal//
ACTOR VisualSpecialEffect
{
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	+NOTELEPORT
}

ACTOR TPortLightning : VisualSpecialEffect
{
	RenderStyle "Add"
	Alpha 0.7
	States
	{
	Spawn:
		TNT1 A 0 // Huh, that's the jump...
		"####" A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72)
	Select:
		BLL1 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
		BLL2 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
		BLL3 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
		BLL4 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
	Fade:
		"----" A 1 BRIGHT A_FadeOut(0.15)
		Loop
	}
}

ACTOR TPortLightningSmall : TPortLightning {Scale 0.1}
ACTOR TPortLightningMedium : TPortLightning {Scale 0.2}
ACTOR TPortLightningLarge : TPortLightning {Scale 0.3}
ACTOR TPortLightningHuge : TPortLightning {Scale 0.4}

// A wave of lightning
ACTOR TPortLightningWave : VisualSpecialEffect
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" A 0 A_SpawnItemEx("TPortLightningSmall",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,0,32)
		"####" A 0 A_SpawnItemEx("TPortLightningMedium",random(-3,3),random(-3,3),random(-3,3),0,0,0,0,0,56)
		"####" A 0 A_SpawnItemEx("TPortLightningLarge",random(-4,4),random(-4,4),random(-3,3),0,0,0,0,0,96)
		"####" A 0 A_SpawnItemEx("TPortLightningHuge",random(-5,5),random(-5,5),random(-5,5),0,0,0,0,0,144)
		Stop
	}
}

ACTOR TPortLightningWaveZap : VisualSpecialEffect
{
	States
	{
	Spawn:
		TNT1 A 0
		"####" A 0 A_SpawnItemEx("TPortLightningSmall",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,0,32)
		"####" A 0 A_SpawnItemEx("TPortLightningSmall",random(-3,3),random(-3,3),random(-3,3),0,0,0,0,0,56)
		"####" A 0 A_SpawnItemEx("TPortLightningSmall",random(-4,4),random(-4,4),random(-3,3),0,0,0,0,0,96)
		"####" A 0 A_SpawnItemEx("TPortLightningSmall",random(-5,5),random(-5,5),random(-5,5),0,0,0,0,0,144)
		Stop
	}
}

// Spawns lightning waves
ACTOR TPortLightningWaveSpawner : VisualSpecialEffect
{
	+RANDOMIZE
	States
	{
	Spawn:
		TNT1 A 0
		"####" A 0 A_PlaySound("TPortalZap")
		"####" A 2 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		"####" A 1 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		"####" A 3 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		"####" A 5 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		"####" A 1 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		"####" A 2 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		"####" A 4 A_SpawnItem("TPortLightningWave")
		Stop
	}
}

ACTOR TPortLightningWaveSpawner2 : VisualSpecialEffect
{
	+RANDOMIZE
	States
	{
	Spawn:
		TNT1 A 0
		"####" A 0 A_PlaySound("TPortalZap")
		"####" A 2 A_SpawnItem("TPortLightningWaveZap")
		"####" A 1 A_SpawnItem("TPortLightningWaveZap")
		"####" A 3 A_SpawnItem("TPortLightningWaveZap")
		"####" A 5 A_SpawnItem("TPortLightningWaveZap")
		"####" A 1 A_SpawnItem("TPortLightningWaveZap")
		"####" A 2 A_SpawnItem("TPortLightningWaveZap")
		"####" A 4 A_SpawnItem("TPortLightningWave")
		Stop
	}
}

//UNSORTED
ACTOR SleepEffect
{
	DistanceCheck "sfxlod"
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+WINDTHRUST
	+NOGRAVITY
	+FLOATBOB
	-SOLID
	RenderStyle "Translucent"
	Alpha 0.001
	Scale 0.2
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(ScaleX*(frandom(0.7,1.0)), ScaleY*(frandom(0.7,1.0)))
		TNT1 A 0 A_Jump(256,1,2)
		SLZZ A 0 A_Jump(256,"End")
		SLZZ A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAAAAAA 1 A_FadeIn(0.05)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAA 2 {A_FadeOut(0.03); A_SetScale(ScaleX*1.05, ScaleY*1.05);}
		Stop
	}
}

ACTOR NaziMedPow : PowerRegeneration
{
	Powerup.Duration -20
	Powerup.Strength 1
}

ACTOR NaziMedicBox : PowerupGiver
{
	Scale 0.65
	Inventory.MaxAmount 1
	Inventory.PickupMessage "$NMBOX"
	Powerup.Type "NaziMedPow"
	-COUNTITEM
	-INVENTORY.INVBAR
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
		MBOX A 1 NoDelay A_SetTics(35*5)
		MBOX A 1 A_FadeOut(0.05)
		Stop
	}
}

ACTOR HitlerChaingun
{
	Scale 1.2
	+FLOORCLIP
	States
	{
	Spawn:
		SHI1 X -1
		Stop
	}
}

ACTOR ToolBox : HitlerChaingun
{
	Scale 0.65
	States
	{
	Spawn:
		TBOX A -1
		Stop
	}
}

ACTOR HitlerSuitScrap : HitlerChaingun
{
	States
	{
	Spawn:
		HIT2 AB 20
		"####" C -1
		Stop
	}
}