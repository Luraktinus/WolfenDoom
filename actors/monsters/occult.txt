//////////
//OCCULT//
//////////
ACTOR BloodSkull : Base
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Bloodskull
	//$Color 4
	Health 150
	Radius 16
	Height 56
	Mass 9999
	Speed 2
	FloatSpeed 1
	PainChance 50
	Scale 0.5
	Monster
	-ISMONSTER //needed for shadows
	+DONTFALL
	+FLOAT
	+NEVERRESPAWN
	+NOGRAVITY
	SeeSound "BSkull/See"
	PainSound "BSkull/Pain"
	DeathSound "BSkull/Death"
	Obituary "$BLOSKULL"
	States
	{
	Spawn:
		SWBS AA 4 A_LookThroughDisguise
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-2,2),random(-2,2),0,0,0,0,0,0)
		Loop
	See:
		SWBS A 1 A_Chase
		SWBS AAA 1 A_Chase("","")
		SWBS A 1 A_Chase
		SWBS AAA 1 A_Chase("","")
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(0,4),random(-2,2),0,0,0,0,0,0)
		Loop
	Missile:
		SWBS A 2 A_FaceTarget
		SWBS B 2 A_SpawnProjectile("BloodSpit",12,0,0,0,0)
		SWBS BA 2
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-2,2),random(-2,2),0,0,0,0,0,0)
		Goto See
	Pain:
		SWBS A 2 A_Pain
		"####" A 0 A_Jump(256,"See")
	XDeath:
	Death:
		SWBS C 6 A_NoBlocking
		SWBS D 5 A_Scream
		TNT1 A 0 A_SetTranslucent(0.8,0)
		SWBS EF 4
		"####" AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BloodSkullCloud",random(-16,16),random(-16,16),random(-24,24),0,0,0.3,0,128)
		SWBS GH 2
		Stop
	}
}

ACTOR WereWaffenSS : WaffenSS
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title WereWaffen SS
	//$Color 4
	Health 160
	Speed 4
	PainChance 128
	Scale 0.65
	SeeSound "wolfman/see"
	PainSound "wolfman/pain"
	DeathSound "wolfman/death"
	Obituary "$WEREWAFF"
	DropItem "9mmAmmoBox" 128
	DropItem "GrenadePickup" 32
	DropItem "MP40" 128
	States
	{
	Spawn:
		WOFF N 0
		Goto Look
	See: //doesn't roll - ozy81
		Goto See.Fast
	Pain:
		"####" E 6 A_NaziPain(256)
		Goto See
	Missile:
		WOFF EF 4 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" G 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",38,6,random(-4,4))
		"####" G 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" F 2 A_MonsterRefire(10,"See")
		"####" F 0 A_FaceTarget
		Goto Missile+2
	Reload:
		WOFF E 0 A_ChangeFlag("NoPain", 1)
		"####" E 20 A_PlaySound("sten/reload", CHAN_ITEM, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" E 0 A_TakeInventory("WaffenSSCounter", 40) //ozy81
		"####" E 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		Goto See
	Death.Fire:
		"####" # 0 A_SetScale(0.62)
		Goto Super::Death.Fire.Random
	Raise:
		"####" MLKJIE 5
		Goto Super::Raise+1
	XDeath:
		SLOP A 1 A_SetScale(0.77)
		Goto Super::XDeath+1
	Disintegrate:
		"####" F 0 A_Playsound("astrostein/guard_death")
		"####" F 5 A_Scream
		"####" F 0 A_NoBlocking
		"####" F 0 A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" F 1 A_FadeOut(0.02)
		Goto Disintegrate+3
	}
}

ACTOR HitlerGhost : Nazi
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Hitler's Ghost
	//$Color 4
	Tag "Hitler's Ghost"
	Health 750
	Mass 1000
	Speed 2.167
	FloatSpeed .67
	PainChance 24
	Scale 0.69
	DamageFactor "Rocket", 0.2
	-FLOORCLIP
	-ISMONSTER //needed for shadows
	+BOSS
	+FLOAT
	+NOBLOOD
	+NOGRAVITY
	Obituary "$SSGHOST"
	DeathSound "hitlerghost/die"
	SeeSound "hitlerghost/see"
	RenderStyle "Translucent"
	States
	{
	Spawn:
		HGST A 0
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	See:
		"####" A 1 A_Chase("Missile","Missile",CHF_FASTCHASE)
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		Loop
	Missile:
		"####" A 4 A_FaceTarget
		"####" D 4 LIGHT(OTTOFIRE) A_SpawnProjectile("EnemyFlamebolt2",36)
		"####" A 4
		Goto See
	Pain:
		"####" A 0 A_Jump(256,"Pain1","Pain2","Pain3")
	Pain1:
		"####" AAA 3 ThrustThing(Angle*256/360-192, 8, 1, 0)
		Goto PainDone
	Pain2:
		"####" AAA 3 ThrustThing(Angle*256/360-64, 8, 1, 0)
		Goto PainDone
	Pain3:
		"####" A 1
	PainDone:
		"####" A 4 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" E 6 A_Scream
		"####" FGHI 6
		"####" J -1 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR Geist : Nazi
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Geist
	//$Color 4
	Health 100
	Mass 1000
	Radius 16
	Scale 0.65
	Speed 5
	FloatSpeed 1.33
	Alpha 0.5
	RenderStyle "Translucent"
	-FLOORCLIP
	-ISMONSTER //needed for shadows
	+DONTFALL
	+FLOAT
	+LOOKALLAROUND
	+NOBLOOD
	+NOGRAVITY
	Obituary "$GEIST"
	PainChance 255
	PainSound "geist/pain"
	DeathSound "geist/die"
	States
	{
	Spawn:
		GEST A 0
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	Look:
		GEST ABCD 4 A_Look
		Loop
	See:
		GEST A 0 A_PlaySound("geist/active", CHAN_VOICE, 1.0, TRUE)
		"####" A 0 A_SetTranslucent(0.1)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.2)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.3)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.4)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.5)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.4)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.3)
		"####" AAABBBCCCDDD 1 A_Chase
		"####" A 0 A_SetTranslucent(0.2)
		"####" AAABBBCCCDDD 1 A_Chase
		Loop
	Melee:
		"####" A 1 A_SetTranslucent(0.1)
		"####" AA 1 A_FadeIn(0.2)
		"####" B 0 A_FaceTarget
		"####" BCDABCD 3 A_CustomMeleeAttack(4)
		"####" AAAA 1 A_Fadeout(0.1)
		Goto See
	Pain:
		"####" E 1 A_Pain
		"####" E 1 A_SetTranslucent(0.4)
		"####" EEE 1 A_Fadeout(0.1)
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" E 0 A_StopSound(CHAN_VOICE)
		"####" E 5 A_SetTranslucent(0.5)
		"####" E 6 A_Scream
		"####" FG 6
		"####" H 6 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR NaziGeist : Geist
{
	//$Title Geist, Nazi Soldier (summon Geists, 6 max)
	Scale 0.66
	Health 400
	Radius 32
	Speed 3.75
	PainChance 128
	States
	{
	Spawn:
		GES2 A 0
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	Look:
		GES2 ABCD 6 A_Look
		Loop
	See:
		GES2 A 0 A_PlaySound("geist/active", CHAN_VOICE, 1.0, TRUE)
		"####" A 0 A_SetTranslucent(0.1)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.2)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.3)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.4)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.5)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.4)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.3)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" A 0 A_SetTranslucent(0.2)
		"####" A 1 A_Chase
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		Loop
	Missile:
		GES2 A 1 BRIGHT A_SetTranslucent(0.1)
		"####" AAABBB 1 BRIGHT A_FadeIn(0.1)
		"####" B 1 BRIGHT A_SetTranslucent(1.0)
		"####" CD 4 BRIGHT A_FaceTarget
		"####" A 0 A_Jump(64,3)
		"####" A 4 BRIGHT A_PainAttack("Geist",random(0,360),PAF_NOSKULLATTACK | PAF_NOTARGET,6)
		"####" A 0 A_Jump(128,3)
		"####" A 0 A_PainAttack("Geist",random(0,360),PAF_NOSKULLATTACK | PAF_NOTARGET,4)
		"####" A 4 BRIGHT A_PainAttack("Geist",random(0,360),PAF_NOSKULLATTACK | PAF_NOTARGET,2)
		"####" B 3 BRIGHT
		"####" BCCCC 1 BRIGHT A_Fadeout(0.1)
		"####" D 4 BRIGHT
		Goto See
	Pain:
		GES2 E 1 A_Pain
		"####" E 1 A_SetTranslucent(0.4)
		"####" EEE 1 A_Fadeout(0.1)
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		GES2 E 0 A_StopSound(CHAN_VOICE)
		"####" E 5 A_SetTranslucent(0.5)
		"####" E 6 A_Scream
		"####" F 5
		"####" G 4 A_NoBlocking
		"####" G 0 A_SpawnItemEx("Stahlhelm",0,8,64,frandom(0.1,-0.1),frandom(0.1,-0.1),frandom(0.1,-0.1),0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR UndeadMonk : HitlerGhost
{
	//$Title Undead Monk
	Health 200
	Scale 0.67
	DamageFactor "Rocket", 1.0
	-BOSS
	-ISMONSTER //needed for shadows
	+LOOKALLAROUND
	Obituary "$UNDEADM"
	DeathSound "monk/death"
	PainSound "monk/pain"
	SeeSound "monk/see"
	States
	{
	Spawn:
		MONK A 0
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	Missile: //mxd. Added cause firing offset
		"####" A 4 A_FaceTarget
		"####" D 4 LIGHT(OTTOFIRE) A_SpawnProjectile("EnemyFlamebolt2",68)
		"####" A 4
		Goto See
	}
}

ACTOR SSCultist : Guard
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title SS Cultist
	Obituary "$SSCULT"
	Health 100
	Mass 125
	Speed 2.66666667
	Scale 0.7
	PainChance 128
	DropItem "12GaugeAmmo" 96
	DropItem "12GaugeAmmo" 128
	DropItem "TrenchShotgun" 32
	PainSound "sscultist/pain"
	DeathSound "sscultist/die"
	SeeSound ""
	States
	{
	Spawn:
		SSCT N 0
		Goto Look
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		SSCT A 0 A_Jump (32, "Run")
		"####" A 0 A_SetSpeed(2.66666667)
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","")
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","")
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","")
		"####" B 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","")
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","")
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","")
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","")
		"####" D 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","")
		"####" E 1 A_NaziChase
		"####" EE 1 A_NaziChase("","")
		"####" E 1 A_NaziChase
		"####" EE 1 A_NaziChase("","")
		Loop
	Run:
		SSCT A 0 A_Jump (24, "See")
		"####" A 0 A_SetSpeed(4.66666667)
		"####" A 1 A_NaziChase
		"####" A 1 A_NaziChase("","")
		"####" A 1 A_NaziChase
		"####" A 1 A_NaziChase("","")
		"####" B 1 A_NaziChase
		"####" B 1 A_NaziChase("","")
		"####" B 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" B 1 A_NaziChase
		"####" B 1 A_NaziChase("","")
		"####" C 1 A_NaziChase
		"####" C 1 A_NaziChase("","")
		"####" C 1 A_NaziChase
		"####" C 1 A_NaziChase("","")
		"####" D 1 A_NaziChase
		"####" D 1 A_NaziChase("","")
		"####" D 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" D 1 A_NaziChase
		"####" D 1 A_NaziChase("","")
		"####" E 1 A_NaziChase
		"####" E 1 A_NaziChase("","")
		"####" E 1 A_NaziChase
		"####" E 1 A_NaziChase("","")
		Loop
	Missile:
		"####" F 4 A_FaceTarget
	Missile.Aimed:
		"####" F 4 A_FaceTarget
		"####" G 0 A_PlaySound("sscultist/shoot", CHAN_6)
		"####" G 0 A_GiveInventory("PriestCounter", 1) //ozy81
		"####" G 0 A_JumpIfInventory("PriestCounter", 12, "Reload") //ozy81
		"####" G 0 A_SpawnProjectile("EnemyShotgunTracer",48,-8,random(-2,2))
		"####" G 0 A_SpawnItemEx("ShotgunCasing", 16,0,60, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 4 LIGHT(PRIESTF)
		"####" F 2 A_MonsterRefire(15,"See")
		"####" F 8 A_FaceTarget
		"####" G 0 A_PlaySound("sscultist/shoot", CHAN_7)
		"####" G 0 A_GiveInventory("PriestCounter", 1) //ozy81
		"####" G 0 A_JumpIfInventory("PriestCounter", 12, "Reload") //ozy81
		"####" G 0 A_SpawnProjectile("EnemyShotgunTracer",48,-8,random(-2,2))
		"####" G 0 A_SpawnItemEx("ShotgunCasing", 16,0,60, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 4 LIGHT(PRIESTF)
		"####" F 8
		Goto See
	Pain:
		SSCT H 6 A_NaziPain(256)
		Goto See
	Reload: //ozy81
		SSCT F 0 A_ChangeFlag("NoPain", 1)
		"####" F 8 A_PlaySound("browning/cock", CHAN_WEAPON, frandom (0.6,0.8), FALSE, ATTN_NORM)
		"####" F 0 A_SpawnItemEx("ShotgunCasing", 16,0,60, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 8 A_PlaySound("browning/cock", CHAN_WEAPON, frandom (0.6,0.8), FALSE, ATTN_NORM)
		"####" F 0 A_SpawnItemEx("ShotgunCasing", 12,0,54, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		"####" F 0 A_TakeInventory("PriestCounter", 12)
		"####" F 0 A_ChangeFlag("NoPain", 0)
		Goto See
	Death:
		SSCT I 6
		"####" J 6 A_Scream
		"####" KL 6
		"####" M 6 A_UnblockAndDrop
		"####" O 6
		"####" P -1
		Stop
	Raise:
		SSCT POMLKJI 6
		Goto See
	Idle:
		SSCT AAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	}
}

ACTOR PlagueDoc1 : Guard
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Plague Doctor (young)
	States
	{
	Spawn:
		PDOC N 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		PDOD A 0
	Dodge.Resume:
		Goto See.Fast //more fast now - ozy81
	Death.Alt1:
		Goto Super::Death+1
	}
}