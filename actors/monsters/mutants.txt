///////////
//MUTANTS//
///////////

ACTOR Mutant : MutantStandard
{
	//$Category Monsters (Wolf3D)/Mutants
	//$Title Mutant (Projectile)
	//$Color 4
	Obituary "$MUTANT"
	Health 90
	+MISSILEEVENMORE
	+MISSILEMORE
	DropItem "9mmAmmo" 128
	States
	{
	Spawn:
		MTNT A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto Look
	See:
		"####" A 0 A_SetSpeed(4)
		Goto See.MutantFasterAlt
	Missile:
		MTNT F 18 A_FaceTarget
		MTNT G 0 A_PlaySound("walther/fire", CHAN_WEAPON)
		MTNT G 7 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer",45,0,random(-1,1))
		MTNT G 0 A_SpawnItemEx("9MMCasing", 0,0,47, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTNT F 8 A_FaceTarget
		MTNT H 0 A_PlaySound("walther/fire", CHAN_WEAPON)
		MTNT H 7 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer",45,0,random(-1,1))
		MTNT H 0 A_SpawnItemEx("9MMCasing", 0,0,47, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTNT F 8 A_MonsterRefire(10,"See")
		MTNT F 0 A_FaceTarget
		Goto Missile+1
	Pain:
		"####" I 6 A_NaziPain(256)
		Goto See
	Death:
		"####" J 3
		"####" K 3 A_Scream
		"####" L 3 A_UnblockAndDrop
		"####" MN 3
		"####" O -1
		Stop
	Idle:
		"####" A 0 A_SetSpeed(1.5)
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		"####" EEEEEEEE 1 A_Wander
		"####" A 0 A_Look
		Loop
	Raise:
		"####" ONMLKJ 4
		Goto See
	}
}

ACTOR MutantMelee : Mutant
{
	//$Title Mutant (Melee)
	DropItem ""
	+LOOKALLAROUND
	States
	{
	Spawn:
		MLMT A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto Look
	See:
		"####" A 0 A_SetSpeed(6)
		Goto See.MutantFasterAlt
	Melee:
		MLMT F 4 A_FaceTarget
		MLMT G 5 A_CustomMeleeAttack(5*random(1,5))
		MLMT F 4 A_FaceTarget
		MLMT H 5 A_CustomMeleeAttack(5*random(1,5))
		Goto See
	Missile:
		MLMT Q 0 A_CheckLOF("Missile.Cleaver", CLOFF_SKIPOBSTACLES | CLOFF_IGNOREGHOST, 1024, 384)
		Goto See
	Missile.Cleaver:
		MLMT Q 8 A_FaceTarget
		MLMT RS 4 A_FaceTarget
		MLMT T 4 A_SpawnProjectile("FlyingCleaver",43,8,random(-2,2))
		MLMT UV 4 A_FaceTarget
		MLMT W 4 A_SpawnProjectile("FlyingCleaver",43,-8,random(-2,2))
		MLMT XY 8 A_FaceTarget
		MLMT Q 4 A_FaceTarget
		Goto See
	}
}

ACTOR SuperMutant : Mutant
{
	//$Title Mutant (Super)
	Obituary "$SUPAMUTN"
	Health 180
	Scale 0.75
	States
	{
	Spawn:
		SUPM A 0 NoDelay ACS_NamedExecuteAlways("DisableSuperMutants",0) //mxd
		Goto Look
	See:
		"####" A 0 A_SetSpeed(5)
		Goto See.MutantFasterAlt
	Missile:
		SUPM F 6 A_FaceTarget
		SUPM G 0 A_PlaySound("supaproj/fire", CHAN_WEAPON)
		SUPM G 4 LIGHT(MUTNFIRE) A_SpawnProjectile("MutantBounceBall",40,random(-22,-20),random(-1,1))
		SUPM G 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		SUPM F 5 A_FaceTarget
		SUPM P 0 A_PlaySound("supaproj/fire", CHAN_WEAPON)
		SUPM P 4 LIGHT(MUTNFIRE) A_SpawnProjectile("MutantBounceBall",40,random(18,20),random(-1,1))
		SUPM P 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		SUPM H 5 A_MonsterRefire(15,"See")
		SUPM H 0 A_FaceTarget
		Goto Missile+1
	}
}

ACTOR BigMutant1 : Mutant
{
	//$Title Giant Mutant (UMG43)
	//$Sprite MTB1A0
	Obituary "$GMUTANT1"
	Health 400
	Scale 1.0
	PainChance 64
	+NEVERFAST
	States
	{
	Spawn:
		MTB1 A 0 NoDelay ACS_NamedExecuteAlways("DisableBigMutants", 0) //mxd
		Goto Look
	See:
		"####" A 0 A_SetSpeed(5)
		Goto See.MutantFaster
	Melee:
		MTB1 F 4 A_FaceTarget
		MTB1 G 5 A_CustomMeleeAttack(8*random(1,5))
		MTB1 F 4 A_FaceTarget
		MTB1 H 5 A_CustomMeleeAttack(8*random(1,5))
		Goto See
	Missile:
		MTB1 E 0 A_Jump(128,"Missile2")
		MTB1 E 0 A_Jump(96,"Missile3")
		MTB1 E 3
		MTB1 E 7 A_FaceTarget
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 F 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, -4, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 F 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 0, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 F 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 4, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 F 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 8, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 F 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 12, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 F 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 16, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
	Missile2:
		MTB1 E 3
		MTB1 E 7 A_FaceTarget
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 G 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 4, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 G 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, 0, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 G 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, -4, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 G 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, -8, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 G 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, -12, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 G 2 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer", 62, 0, -16, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
	Missile3:
		MTB1 E 4
		MTB1 E 10 A_FaceTarget
		MTB1 F 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 FFF 0 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer",64,0,frandom(-4,4))
		MTB1 FFF 0 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 EG 3 A_FaceTarget
		MTB1 G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		MTB1 GGG 0 LIGHT(MUTNFIRE) A_SpawnProjectile("EnemyMutantTracer",64,0,frandom(-4,4))
		MTB1 GGG 0 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 EF 3 A_FaceTarget
		Goto See
	Death:
		"####" I 5
		"####" J 5
		"####" K 5 A_Scream
		"####" L 5 A_UnblockAndDrop
		"####" M -1
		Stop
	Idle:
		"####" A 0 A_SetSpeed(2.5)
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		"####" EEEEEEEE 1 A_Wander
		"####" A 0 A_Look
		Loop
	Death.Fire:
		"####" # 0 A_SetScale(0.65)
		Goto Death.Fire.Random
	Raise:
		"####" MMMLKJI 4
		Goto See
	XDeath:
		Goto XDeath.Big
	Pain:
		"####" H 6 A_NaziPain(256)
		Goto See
	}
}

ACTOR BigMutant2 : BigMutant1
{
	//$Title Giant Mutant (MutantRockets)
	//$Sprite MTB2A0
	Obituary "$GMUTANT2"
	Health 450
	PainChance 64
	+MISSILEEVENMORE
	DropItem "NebAmmo", 128, 2
	States
	{
	Spawn:
		MTB2 A 0 NoDelay ACS_NamedExecuteAlways("DisableBigMutants", 0) //mxd
		Goto Look
	Melee:
		MTB2 E 4 A_FaceTarget
		MTB2 F 5 A_CustomMeleeAttack(8*random(1,5))
		MTB2 E 4 A_FaceTarget
		MTB2 G 5 A_CustomMeleeAttack(8*random(1,5))
		Goto See
	Missile:
		MTB2 E 9 A_FaceTarget
		MTB2 E 0 A_Jump(96,"Missile2")
		MTB2 E 0 A_Jump(192,"Missile3")
		MTB2 F 4 A_SpawnProjectile("MutantRocket",62,0,random(-8,-16))
		MTB2 E 9 A_FaceTarget
		Goto See
	Missile2:
		MTB2 G 4 A_SpawnProjectile("MutantRocket",62,0,random(8,16))
		MTB2 E 9 A_FaceTarget
		Goto See
	Missile3:
		MTB2 F 0 A_SpawnProjectile("MutantRocket", 62,0,random(-8,-16))
		MTB2 F 0
		MTB2 G 5 A_SpawnProjectile("MutantRocket", 62,0, 0)
		MTB2 E 9 A_FaceTarget
		Goto See
	}
}

ACTOR UberMutant : Mutant
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Ubermutant (Boss)
	//$Color 4
	Tag "Ubermutant"
	Health 1000
	Radius 24
	Height 64
	Mass 1000
	Speed 2.66666667
	PainChance 50
	DamageFactor "Rocket", 0.2
	+BOSS
	+LOOKALLAROUND
	Obituary "$OB_BARON"
	HitObituary "$UBERMUTN"
	SeeSound "baron/sight"
	DeathSound "baron/death"
	States
	{
	Spawn:
		UBMU A 0 NoDelay ACS_NamedExecuteAlways("DisableSuperMutants", 0) //mxd
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	Look:
		UBMU AB 10 A_Look
		Loop
	See:
		Goto See.BossFast
	Melee:
	Missile:
		UBMU EF 8 A_FaceTarget
		UBMU G 0 A_Jump(64,"Chaingun")
		UBMU G 0 A_Jump(128,"Missile2")
		UBMU G 0 A_Jump(192,"Missile3")
		UBMU G 4 LIGHT(MUTNROCK) A_SpawnProjectile("MutantRocket",44,0,0)
		Goto See
	Missile2:
		UBMU G 4 LIGHT(MUTNROCK) A_SpawnProjectile("MutantRocket",44,0,random(8,16))
		UBMU EF 5 A_FaceTarget
		UBMU G 4 LIGHT(MUTNROCK) A_SpawnProjectile("MutantRocket",44,0,0)
		Goto See
	Missile3:
		UBMU G 0 LIGHT(MUTNROCK) A_SpawnProjectile("MutantRocket",44,0,random(-8,-16))
		UBMU G 0 LIGHT(MUTNROCK) A_SpawnProjectile("MutantRocket",44,0, 0)
		UBMU G 5 LIGHT(MUTNROCK) A_SpawnProjectile("MutantRocket",44,0,random(8,16))
		UBMU EF 5 A_FaceTarget
		Goto See
	Chaingun:
		UBMU G 0 A_FaceTarget
		UBMU G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-1,1))
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-4,4))
		UBMU GG 0 A_SpawnItemEx("9MMCasing", 0,0,46, RandomPick(-8, 8), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		UBMU G 3 LIGHT(MUTNFIRE)
		UBMU G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-1,1))
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-4,4))
		UBMU GG 0 A_SpawnItemEx("9MMCasing", 0,0,46, RandomPick(-8, 8), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		UBMU G 3 LIGHT(MUTNFIRE)
		UBMU G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-1,1))
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-4,4))
		UBMU GG 0 A_SpawnItemEx("9MMCasing", 0,0,46, RandomPick(-8, 8), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		UBMU G 3 LIGHT(MUTNFIRE)
		UBMU G 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-1,1))
		UBMU G 0 A_SpawnProjectile("EnemyMutantTracer",44,0,random(-4,4))
		UBMU GG 0 A_SpawnItemEx("9MMCasing", 0,0,46, RandomPick(-8, 8), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		UBMU G 3 LIGHT(MUTNFIRE)
		UBMU G 4 A_MonsterRefire(20,"See")
		Goto Missile+2
	Pain:
		UBMU H 6 A_NaziPain(256)
		Goto See
	Death:
		UBMU I 6
		UBMU I 6 A_Scream
		UBMU J 6
		UBMU J 6 A_UnblockAndDrop
		UBMU KL 6
		UBMU M -1 A_BossDeath
		Stop
	Raise:
		UBMU MLKJJII 6
		Goto See
	}
}