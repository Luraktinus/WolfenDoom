ACTOR ROfficer : Guard
{
	//$Category Monsters (Wolf3D)/Allied Soldiers
	//$Title NKVD Russian Officer
	Health 50
	Speed 3.0
	DropItem ""
	Species "Ally"
	+FRIENDLY
	Obituary "$ROFFICER"
	SeeSound "urss/sighted"
	PainSound "urss/pain"
	DeathSound "urss/death"
	States
	{
	Spawn:
		NKVO N 1
		Goto Look
	See:
		Goto See.Faster
	Missile: //1 shoot x round
		"####" E 4 A_FaceTarget
	Missile.Aimed:
		"####" F 4 A_FaceTarget
		"####" G 0 A_PlaySound("urss/tokarev", CHAN_WEAPON)
		"####" G 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyPistolTracer",52,1,random(-4,4))
		"####" G 0 A_GiveInventory("PistolGuardCounter", 1)
		"####" F 5 A_SpawnItemEx("9MMCasing", 1,0,54, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_JumpIfInventory("PistolGuardCounter", 8, "Reload")
		"####" F 0 A_Jump(256,"See")
		Stop
	Reload:
		"####" E 0 A_ChangeFlag("NoPain", 1)
		"####" E 25 A_PlaySound("urss/tokarelo", CHAN_ITEM, frandom (0.3,0.6), FALSE, ATTN_NORM)
		"####" E 0 A_TakeInventory("PistolGuardCounter", 8)
		"####" E 0 A_SpawnItemEx("9MMCasing", 1, 0, 56, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		"####" "#" 0 A_Jump(256, "See")
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_UnblockAndDrop
		"####" L 5
		"####" M -1
		Stop
	}
}

ACTOR RPPSHGuard1 : MP40Guard
{
	//$Category Monsters (Wolf3D)/Allied Soldiers
	//$Title NKVD Russian PPSH Guard (Winter Hat)
	Health 50
	Speed 2.5
	DropItem ""
	Species "Ally"
	+FRIENDLY
	Obituary "$RPGUARDS"
	SeeSound "urss/sighted"
	PainSound "urss/pain"
	DeathSound "urss/death"
	States
	{
	Spawn:
		NKVF N 1
		Goto Look
	See:
		Goto See.Normal
	Missile:
		"####" E 3 A_FaceTarget
	Missile.Aimed:
		"####" E 3 A_FaceTarget
		"####" F 0 A_PlaySound("urss/ppsh", CHAN_WEAPON)
		"####" F 3 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",40,4,random(-8,8))
		"####" F 0 A_GiveInventory("WMP40GuardCounter", 1)
		"####" F 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 2 A_FaceTarget
		"####" G 0 A_PlaySound("urss/ppsh", CHAN_WEAPON)
		"####" G 3 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemySMGTracer",40,4,random(-12,12))
		"####" G 0 A_GiveInventory("WMP40GuardCounter", 1)
		"####" G 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_JumpIfInventory("WMP40GuardCounter", 32, "Reload")
		"####" E 3 A_MonsterRefire(10,"See")
		"####" E 0 A_FaceTarget
		Goto Missile + 2
	Reload:
		NKVF E 0 A_ChangeFlag("NoPain", 1)
		"####" E 40 A_PlaySound("urss/ppshrelo", CHAN_WEAPON)
		"####" E 0 A_TakeInventory("WMP40GuardCounter", 32)
		"####" E 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		Goto See
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_UnblockAndDrop
		"####" L 5
		"####" M -1
		Stop
	}
}

ACTOR RPPSHGuard2 : RPPSHGuard1
{
	//$Title NKVD Russian PPSH Guard (Peak)
	Speed 2.2
	States
	{
	Spawn:
		NKVP N 1
		Goto Look
	Reload:
		NKVP E 0 A_ChangeFlag("NoPain", 1)
		"####" E 40 A_PlaySound("urss/ppshrelo", CHAN_WEAPON)
		"####" E 0 A_TakeInventory("WMP40GuardCounter", 32)
		"####" E 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		Goto See
	}
}

ACTOR RPPSHGuard3 : RPPSHGuard1
{
	//$Title NKVD Russian PPSH Guard (Trench)
	Speed 1.5
	States
	{
	Spawn:
		NKVT N 1
		Goto Look
	Reload:
		NKVT E 0 A_ChangeFlag("NoPain", 1)
		"####" E 30 A_PlaySound("urss/ppshrelo", CHAN_WEAPON)
		"####" E 0 A_TakeInventory("WMP40GuardCounter", 32)
		"####" E 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		Goto See
	}
}

ACTOR RDragunovGuard : RPPSHGuard1
{
	//$Title NKVD Russian Dragunov Guard (Helmet)
	Obituary "$RDRAGNOV"
	Speed 1.7
	States
	{
	Spawn:
		NKVR N 1
		Goto Look
	Missile:
		"####" E 10 A_FaceTarget
	Missile.Aimed:
		"####" F 10 A_FaceTarget
		"####" G 0 A_PlaySound("urss/dragunov", CHAN_WEAPON)
		"####" G 8 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyRifleTracer",40,8,random(-1,1))
		"####" G 0 A_GiveInventory("RifleGuardCounter", 1)
		"####" G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 8
		"####" F 0 A_JumpIfInventory("RifleGuardCounter", 10, "Reload")
		Goto See
	Reload:
		NKVR E 0 A_ChangeFlag("NoPain", 1)
		"####" E 45 A_PlaySound("urss/dragrelo", CHAN_ITEM, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" E 0 A_TakeInventory("RifleGuardCounter", 10)
		"####" E 0 A_SpawnItemEx("MauserRifleCasing", 8,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		Goto See
	}
}