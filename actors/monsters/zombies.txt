ACTOR ZombieKazi : ZombieStandard
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Nazi Zombie, Kamikaze
	//$Color 4
	Health 100
	Speed 8
	+NOVERTICALMELEERANGE
	Obituary "$NAZOKAZI"
	DropItem "9mmAmmo" 64
	DropItem "GrenadePickup" 16
	States
	{
	Spawn:
		ZKAZ N 0
		Goto Look
	Melee:
		"####" E 0 A_Die
		Stop
	Missile:
		"####" E 0 A_JumpIfCloser(192,"Melee",TRUE)
		"####" EF 20 A_FaceTarget
		"####" G 0 A_PlaySound("nazombie/vomit", CHAN_WEAPON)
		"####" G 12 A_SpawnProjectile("ZombieVomit",54,0,0, CMF_AIMDIRECTION)
		"####" E 8
		Goto See
	XDeath: //what a fall through :P
	Death.Fire:
	Disintegrate:
	Death:
		"####" F 5 
		"####" I 0 A_SpawnItemEx("ZombieNuke",0,0,0)
		"####" IIIIIIIIIIIIIIIIIIIIIIIIIIIIIIII 0 A_SpawnItemEx("Debris_Bone", random(8,-8), random(8,-8), random(54,64), random(1,6), random(1,6), random(1,6), random(0,360), SXF_CLIENTSIDE)
		"####" I 1 {A_SpawnItemEx("Debris_Skull", random(8,-8), random(8,-8), random(54,64), random(1,6), random(1,6), random(1,6), random(0,360), SXF_CLIENTSIDE); A_NoBlocking;}
		"####" J 4 A_SpawnItemEx("ZombieSoul", 0, 0, 10, 0, 0, frandom(1,3))
		"####" KLM 4
		"####" U -1 {A_UnSetSolid; A_SetFloorclip;}
		Stop
	}
}

ACTOR ZombieBrain : ZombieStandard
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Nazi Zombie, Brain Eater
	//$Color 4
	Health 80
	Speed 2
	+QUICKTORETALIATE
	Obituary "$NAZOBITE"
	DropItem "9mmAmmo" 32
	States
	{
	Spawn:
		ZBIT N 0
		Goto Look
	See:
		"####" "#" 0 A_JumpIfCloser (radius + 320, "Run")
		"####" "#" 0 A_SetSpeed(2)
		Goto See.Normal
	Run:
		"####" "#" 0 A_SetSpeed(6)
	RunLoop:
		"####" O 1 A_Chase
		"####" OOO 1 A_Chase("","")
		"####" O 1 A_Chase
		"####" OOO 1 A_Chase("","")
		"####" P 1 A_Chase
		"####" P 0 A_SpawnItemEx("EnemyStep", 0, 0, 14, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" PPP 1 A_Chase("","")
		"####" P 1 A_Chase
		"####" PPP 1 A_Chase("","")
		"####" Q 1 A_Chase
		"####" QQQ 1 A_Chase("","")
		"####" Q 1 A_Chase
		"####" QQQ 1 A_Chase("","")
		"####" R 1 A_Chase
		"####" R 0 A_SpawnItemEx("EnemyStep", 0, 0, 14, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" RRR 1 A_Chase("","")
		"####" R 1 A_Chase
		"####" RRR 1 A_Chase("","")
		Goto See
	Melee:
		"####" F 4 A_FaceTarget
		"####" G 0 A_PlaySound("nazombie/pain", CHAN_ITEM)
		"####" G 5 A_CustomMeleeAttack(5*random(2,4))
		"####" F 4 A_FaceTarget
		"####" G 0 A_PlaySound("nazombie/pain", CHAN_ITEM)
		"####" G 5 A_CustomMeleeAttack(5*random(2,4))
		Goto See
	Missile:
		"####" EF 20 A_FaceTarget
		"####" G 0 A_PlaySound("nazombie/vomit", CHAN_WEAPON)
		"####" G 12 A_SpawnProjectile("ZombieVomit",54,0,0, CMF_AIMDIRECTION)
		"####" E 8
		Goto See
	Pain:
		"####" F 6 A_NaziPain(256)
		"####" F 0 A_Jump(256,"See")
	Death:
		"####" I 5 
		"####" J 5 A_Scream
		"####" K 5 A_UnblockAndDrop
		"####" L 5 A_SpawnItemEx("ZombieSoul", 0, 0, 10, 0, 0, frandom(1,3))
		"####" M -1
		Stop
	Raise:
		"####" MLKJI 5
		"####" I 0 A_Jump(256,"See")
		Stop
	}
}

ACTOR ZombieOfficer : ZombieBrain
{
	//$Title Nazi Zombie, Officer (MP40)
	Speed 3
	-QUICKTORETALIATE
	+AVOIDMELEE
	DropItem "MP40" 192
	DropItem "9mmAmmo" 32
	States
	{
	Spawn:
		ZOFF N 0
		Goto Look
	See:
		Goto See.Normal
	Melee:
	Missile:
		ZOFF E 15 A_FaceTarget
		"####" F 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" F 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,18,90,CMF_AIMDIRECTION)
		"####" F 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" F 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" F 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" F 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,16,80,CMF_AIMDIRECTION)
		"####" F 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" F 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" F 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" F 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,14,70,CMF_AIMDIRECTION)
		"####" F 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" F 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" G 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" G 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,12,60,CMF_AIMDIRECTION)
		"####" G 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" G 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" G 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,10,50,CMF_AIMDIRECTION)
		"####" G 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" G 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" G 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,8,40,CMF_AIMDIRECTION)
		"####" G 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" H 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" H 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,6,30,CMF_AIMDIRECTION)
		"####" H 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" H 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" H 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" H 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" H 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,4,15,CMF_AIMDIRECTION)
		"####" H 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" H 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" H 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" H 0 A_PlaySound("nazi/stg44", CHAN_WEAPON, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" H 6 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyStGTracer",30,2,0)
		"####" H 0 A_GiveInventory("WaffenSSCounter", 1) //ozy81
		"####" H 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" H 0 A_JumpIfInventory("WaffenSSCounter", 40, "Reload") //ozy81
		"####" E 8 A_MonsterRefire(10,"See")
		"####" E 0 A_FaceTarget
		Goto Missile+2
	Reload:
		ZOFF E 0 A_ChangeFlag("NoPain", 1)
		"####" E 25 A_PlaySound("sten/reload", CHAN_ITEM, frandom (0.6,0.9), FALSE, ATTN_NORM)
		"####" E 0 A_TakeInventory("WaffenSSCounter", 40) //ozy81
		"####" E 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" E 0 A_ChangeFlag("NoPain", 0)
		Goto See
	Pain:
		"####" I 6 A_NaziPain(256)
		"####" I 0 A_Jump(256,"See")
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_UnblockAndDrop
		"####" L 5 A_SpawnItemEx("ZombieSoul", 0, 0, 10, 0, 0, frandom(1,3))
		"####" M 5
		"####" O -1
		Stop
	Raise:
		"####" OMLKJI 5
		"####" I 0 A_Jump(256,"See")
		Stop
	}
}

ACTOR Camp_PrisonerZombie : NaziStandard //can't inherit ZombieBrain because of messed up translations result - ozy81
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Zombie Prisoner
	//$Color 4
	Health 40
	Speed 3
	Radius 20
	Height 50
	Mass 80
	GibHealth 30
	PainChance 64
	Monster
	BloodColor "Black"
	BloodType "UndeadBlood"
	DamageFactor "Fire", 1.2
	DamageFactor "MutantPoison", 0
	DamageFactor "Normal", 0.5
	DamageFactor "Poison", 0
	DamageFactor "UndeadPoison", 0
	MaxStepHeight 16
	MaxDropoffHeight 32
	Obituary "$NAZOPRIS"
	SeeSound "nazombie/sighted"
	PainSound "nazombie/pain"
	DeathSound "sscultist/die"
	States
	{
	Spawn:
		CZOM N 0
		Goto Look
	See:
		CZOM "#" 0 A_JumpIfCloser (radius + 160, "Run")
		"####" "#" 0 A_SetSpeed(3)
		Goto See.Normal
	Run:
		"####" "#" 0 A_SetSpeed(6)
	RunLoop:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 0 A_SpawnItemEx("EnemyStep", 0, 0, 14, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" BBB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 0 A_SpawnItemEx("EnemyStep", 0, 0, 14, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" DDD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","")
		Goto See
	Melee:
		"####" EF 4 A_FaceTarget
		"####" G 0 A_PlaySound("nazombie/pain", CHAN_ITEM)
		"####" G 5 A_CustomMeleeAttack(4*random(1,3))
		"####" F 4 A_FaceTarget
		"####" G 0 A_PlaySound("nazombie/pain", CHAN_ITEM)
		"####" G 5 A_CustomMeleeAttack(4*random(1,3))
		Goto See
	Pain:
		CZOM H 6 A_NaziPain(256)
		"####" H 0 A_Jump(256,"See")
		Goto Spawn
	Death:
		"####" I 5 A_Scream
		"####" J 6
		"####" K 5 A_NoBlocking
		"####" LM 4
		"####" O 5 A_SpawnItemEx("ZombieSoul", 0, 0, 10, 0, 0, frandom(1,3))
		"####" P -1
		Stop
	}
}

ACTOR ZombieGeneral : RocketMan
{
	//$Title General Fettgesicht, Zombiefied (Boss)
	Base.BossIcon "BOSSICO2"
	Tag "Undead Fettgesicht"
	Health 1000
	Obituary "$ZGENERAL"
	SeeSound "nazombie/sighted"
	PainSound "nazombie/pain"
	DeathSound "sscultist/die"
	BloodColor "Black"
	BloodType "UndeadBlood"
	DropItem "NebAmmoBox" 128
	DropItem "NebAmmoBox" 128
	States
	{
	Spawn:
		ZFET A 0
		Goto Look
	Missile:
		ZFET E 10 A_FaceTarget
		"####" E 0 A_Jump(96,"ChainGun","GasBomb")
		"####" F 8 LIGHT(OTTOFIRE) A_SpawnProjectile("ZombieRocket",43,24)
		"####" E 8 A_FaceTarget
		"####" F 8 LIGHT(OTTOFIRE) A_SpawnProjectile("ZombieRocket",43,24)
		"####" E 8 A_FaceTarget
		"####" F 8 LIGHT(OTTOFIRE) A_SpawnProjectile("ZombieRocket",43,24)
		"####" E 8
		Goto See
	ChainGun:
		ZFET H 0 A_FaceTarget
		"####" H 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		"####" H 2 LIGHT(SUPAFIRE) A_SpawnProjectile("EnemyChaingunTracer",40,-20,random(-11,11))
		"####" G 0 A_SpawnItemEx("9MMCasing", -12,0,36, random(-3,-4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_MonsterRefire(10,"See")
		Loop
	GasBomb:
		ZFET F 4 LIGHT(OTTOFIRE) A_SpawnProjectile("GasBomb",43,24,random(-16,16))
		"####" E 8
		Goto Missile
	Pain:
		ZFET I 6 A_NaziPain(256)
		Goto See
	Death:
		ZFET J 5
		"####" K 5 A_Scream
		"####" L 5 A_SpawnItemEx("ZombieSoul", 0, 0, 10, 0, 0, frandom(1,3))
		"####" M -1 A_NoBlocking
		Stop
	Raise:
		ZFET MLKJ 5
		Goto See
	}
}

ACTOR ZombieZyklonstein : RocketMan
{
	//$Title Zyklonstein (Boss)
	Base.BossIcon "BOSSICO2"
	Tag "Zyklonstein"
	Health 1200
	Obituary "$ZZYKLON"
	SeeSound "nazombie/sighted"
	PainSound "nazombie/pain"
	DeathSound "sscultist/die"
	DropItem "FlameAmmo" 128
	DropItem "FlameAmmo" 128
	States
	{
	Spawn:
		ZSKL A 0
		Goto Look
	Missile:
		ZSKL E 0 A_Jump(128,"Chaingun")
		"####" E 10 A_FaceTarget
		"####" E 0 A_JumpIfCloser(384,"ZyklonFlame")
		"####" F 4 LIGHT(ZYKOFIRE) A_SpawnProjectile("ZFlameBall",44,12)
		"####" E 6
		Goto See
	ZyklonFlame:
		ZSKL FFFFF 1 LIGHT(ZYKOFIRE) A_SpawnProjectile("ZFlamebolt",44,12)
		"####" E 6 A_MonsterRefire(10,"See")
		Goto Missile
	Chaingun:
		ZSKL H 0 A_FaceTarget
		"####" H 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		"####" H 2 LIGHT(SUPAFIRE) A_SpawnProjectile("EnemyChaingunTracer",40,-20,random(-11,11))
		"####" G 0 A_SpawnItemEx("9MMCasing", -12,0,36, random(-3,-4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" G 2 A_MonsterRefire(10,"See")
		Loop
	Pain:
		ZSKL I 6 A_NaziPain(256)
		Goto See
	Death:
		ZSKL J 5
		"####" K 5 A_Scream
		"####" L 5 A_SpawnItemEx("ZombieSoul", 0, 0, 10, 0, 0, frandom(1,3))
		"####" M -1 A_NoBlocking
		Stop
	Raise:
		ZSKL MLKJ 5
		Goto See
	}
}