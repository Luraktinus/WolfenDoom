///////////
//ATTACKS//
///////////

ACTOR AstroDroneBall
{
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage 5
	Projectile
	+RANDOMIZE
	RenderStyle "Add"
	Scale 0.5
	SeeSound "astrochaingun/fire"
	DeathSound "astroplasmaexplode"
	States
	{
	Spawn:
		ROBP AB 4 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		ROBP CDEG 6 BRIGHT
		Stop
	}
}

ACTOR HardAstroDroneBall : AstroDroneBall {+MTHRUSPECIES}

ACTOR AstroRobotSeekerBall : AstroDroneBall
{
	Radius 12
	Height 16
	Speed 7
	MaxTargetRange 128
	Scale 1.2
	+SEEKERMISSILE
	States
	{
	Spawn:
		ROBP A 0 NODELAY A_Jump(2,"PreciseLoop")
		"####" AB 4 A_SeekerMissile(8, random(15,30))
		Loop
	PreciseLoop:
		"####" AB 4 A_SeekerMissile(8, random(60,90), SMF_PRECISE)
		Loop
	}
}

ACTOR HardAstroRobotSeekerBall : AstroRobotSeekerBall {+MTHRUSPECIES}

ACTOR Destroyed_AstroSuite
{
	//$Category Props (Wolf3D)/Astrostein
	//$Title Astrostein Suite Guard Carcass (destroyable)
	//$Color 3
	Radius 16
	Height 56
	Health 50
	Mass 400
	Scale 0.70
	+FRIENDLY
	+ISMONSTER
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		ROB1 O -1
		Stop
	Death:
		ROB1 O 1 {A_Scream; A_PlaySound("astrostein/explosion", CHAN_AUTO, 1.0, FALSE, 0.1); A_SpawnItemEx("AstrosteinExplosion_Decorative", 0, 0, 40); A_NoBlocking;}
		//"####" A 0 A_SpawnItemEx("NebNuke", 0, 0, 32, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" OO 0 A_SpawnItemEx("Debris_AstroSuite", random(8,40), random(16,44), random(32,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOO 0 A_SpawnItemEx("Debris_AstroSuite", random(16,56), random(32,56), random(56,64), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOOO 0 A_SpawnItemEx("Debris_AstroSuite", random(32,64), random(64,80), random(16,32), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOOOO 0 A_SpawnItemEx("Debris_MetalJunk", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOOOOOO 0 A_SpawnItemEx("Debris_MetalJunk", random(16,32), random(32,48), random(64,80), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR Destroyed_AstroRobot : Destroyed_AstroSuite
{
	//$Title Astrostein Robot Carcass (destroyable)
	//$Color 3
	Radius 32
	States
	{
	Spawn:
		ROB2 O -1
		Stop
	Death:
		ROB2 O 1 {A_Scream; A_NoBlocking;}
		//"####" A 0 A_SpawnItemEx("NebNuke", 0, 0, 32, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		"####" OO 0 A_SpawnItemEx("Debris_AstroRobot", random(8,40), random(16,44), random(32,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOO 0 A_SpawnItemEx("Debris_AstroRobot", random(16,56), random(32,56), random(56,64), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOOO 0 A_SpawnItemEx("Debris_AstroRobot", random(32,64), random(64,80), random(16,32), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOOOO 0 A_SpawnItemEx("Debris_MetalJunk", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" OOOOOOO 0 A_SpawnItemEx("Debris_MetalJunk", random(16,32), random(32,48), random(64,80), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR AstroSuiteDropper : RandomSpawner { DropItem Destroyed_AstroSuite }
ACTOR AstroRobotDropper : RandomSpawner { DropItem Destroyed_AstroRobot }

////////////////
//DEATH EFFECT//
////////////////

ACTOR BaseLine
{
	+BRIGHT
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	Radius 0
	Height 0
	RenderStyle "Add"
	Alpha 0.01
	States
	{
	Spawn:
		SPFX AAAAA 1 Light("AstrosteinDeathLight") A_FadeIn(0.2)
		"####" A 1 Light("AstrosteinDeathLight") A_FadeOut(0.06)
		Wait
	}
}

////////////////
//REPLACEMENTS//
////////////////

ACTOR TeleportFog_Astro : TeleportFog replaces TeleportFog
{
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 16), 0, 0, random(1,3), 0, 129, 0)
		Stop
	}
}

///////////
//ENEMIES//
///////////

ACTOR AstroGuard : Guard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Guard
	//$Color 4
	Scale 0.70
	DropItem "AstroClipAmmo" 128
	DropItem "AstroShotgunShell" 32
	Obituary "$AGUARD1"
	States
	{
	Spawn:
		ATR2 A 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		ATD2 A 0
	Dodge.Resume:
		Goto See.Normal
	Missile:
	Missile.Aimed:
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 8 A_Jump(256,"See")
		Stop
	Pain:
		"####" G 6 A_NaziPain(256)
		"####" G 0 A_Jump(256,"See")
		Stop
	Death.Fire: //We don't want different Death sequences than the Disintegrate one for Astrosteins, yeah?
	XDeath:
	Death:
		"####" A 0 A_Jump(256,"Disintegrate")
	}
}

ACTOR AstroGuard2 : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Guard 2
	//$Color 4
	Health 30
	Scale 0.70
	States
	{
	Spawn:
		ATR1 A 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		ATD1 A 0
	Dodge.Resume:
		Goto See.Normal
	Missile:
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 8 A_Jump(256,"See")
		Stop
	}
}

ACTOR AstroCommando : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Commando
	//$Color 4
	Health 80
	Scale 0.65
	Obituary "$AGUARD2"
	States
	{
	Spawn:
		ATR3 A 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		ATD3 A 0
	Dodge.Resume:
		Goto See.Normal
	Missile:
		"####" E 5 A_FaceTarget
	Missile.Aimed:
		"####" F 5 A_FaceTarget
		"####" G 0 A_PlaySound("astrochaingun/fire")
		"####" G 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" F 5 A_MonsterRefire(30,"See")
		Goto Missile+1
	Pain:
		"####" H 6 A_NaziPain(256)
		"####" H 0 A_Jump(256,"See")
		Stop
	}
}

ACTOR AstroOfficer : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Officer
	//$Color 4
	Speed 5.33
	Health 30
	Scale 0.67
	Obituary "$AGUARD3"
	States
	{
	Spawn:
		ATR5 A 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		ATD5 A 0
	Dodge.Resume:
		Goto See.Normal
	Missile:
	Missile.Aimed:
		"####" G 5 A_FaceTarget
		"####" G 0 A_PlaySound("astrochaingun/fire")
		"####" E 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",32,1,random(-8,8))
		"####" F 5 A_MonsterRefire(30,"See")
		"####" E 8 A_Jump(256,"See")
		Stop
	}
}

ACTOR AstroElite : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Elite
	//$Color 4
	Speed 5.0
	Health 30
	Scale 0.63
	DropItem "AstroShotgun" 64
	DropItem "AstroShotgunShell" 192
	Obituary "$AGUARD4"
	States
	{
	Spawn:
		ATR4 A 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		ATD4 A 0
	Dodge.Resume:
		Goto See.Fast //now more faster - ozy81
	Missile:
		"####" F 8 A_FaceTarget
	Missile.Aimed:
		"####" G 8 A_FaceTarget
		"####" H 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" H 3 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" H 3 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-4,4))
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" H 3 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" G 2 A_FaceTarget
		"####" H 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" H 3 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-4,4))
		Goto See
	Pain:
		"####" I 6 A_NaziPain(256)
		"####" I 0 A_Jump(256,"See")
		Stop
	Death.Fire: //We don't want different Death sequences than the Disintegrate one for Astrosteins, yeah?
	XDeath:
	Death:
		ATR4 I 0 A_Jump(256,"Disintegrate")
	}
}

ACTOR AstroSuiteGuard : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Suite Guard
	//$Color 4
	Speed 2
	Scale 0.70
	Health 200
	Painchance 16
	+LOOKALLAROUND
	+NOBLOOD
	States
	{
	Spawn:
		ROB1 A 0
		Goto Look
	Missile:
	Missile.Aimed:
		"####" EF 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" G 0 A_SpawnProjectile("EnemyAstroTracer",48,-10,random(-8,4))
		"####" G 0 A_SpawnProjectile("EnemyAstroTracer",40,-10,random(-8,4))
		"####" G 0 A_SpawnProjectile("EnemyAstroTracer",40,10,random(4,-8))
		"####" G 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,10,random(4,-8))
		"####" E 8 A_Jump(256,"See")
		Stop
	Pain:
		"####" H 3
		"####" H 3 A_Pain
		"####" H 0 A_Jump(256,"See")
		Stop
	Death.Fire: //We don't want different Death sequences than the Disintegrate one for Astrosteins, yeah?
	XDeath:
	Death:
	Disintegrate:
		"####" I 0 A_Playsound("astrostein/guard_death")
		"####" I 0 A_Scream
		"####" IIIIIJJJJJKKKKKLLLLLMMMMMNNNNNOOOOO 1 A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" O 1 A_SpawnItemEx("AstroSuiteDropper", 0, 0, 0)
		Stop
	}
}

ACTOR AstroScientist : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astro Scientist
	//$Color 4
	Scale 0.67
	States
	{
	Spawn:
		SCN2 N 0
		Goto Look
	See:
		Goto See.Dodge
	Dodge:
		SCD2 A 0
	Dodge.Resume:
		Goto See.Normal
	Missile: //different brightmap & colors - ozy81
		"####" E 5 A_FaceTarget
	Missile.Aimed:
		"####" F 5 A_FaceTarget
		"####" O 0 A_PlaySound("astrochaingun/fire")
		"####" O 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 8 A_Jump(256,"See")
		Stop
	}
}

ACTOR AstroDrone : Nazi
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Drone
	//$Color 4
	Scale 0.5
	Health 200
	Radius 31
	Mass 400
	Speed 2.66666667
	PainChance 128
	-FLOORCLIP
	-ISMONSTER //needed for shadows
	+FLOAT
	+LOOKALLAROUND
	+NOBLOOD
	+NOGRAVITY
	SeeSound "drone_sight"
	PainSound "drone_pain"
	DeathSound "astrostein/explosion"
	ActiveSound "drone_act"
	Obituary "$ADRONE"
	States
	{
	Spawn:
		DRON A 0
		Goto Look
	See:
		DRON A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		Loop
	Idle:
		DRON AAABBB 1 A_Wander
		"####" A 0 A_Look
		Loop
	Missile:
	Missile.Aimed:
		DRON EG 5 A_FaceTarget
		"####" E 5 A_SpawnProjectile("AstroDroneBall",54,1,random(-8,8))
		Goto See
	Pain:
	Pain.Dagger:
		DRON H 3
		"####" H 3 A_Pain
		"####" H 6
		Goto See
	Death:
		DRON M 0 A_Scream
		"####" M 0 A_SpawnItemEx("AstrosteinExplosion_Medium", 0, 0, 48)
		"####" M 0 {A_NoBlocking; A_ChangeFlag("NOGRAVITY",FALSE);}
		"####" M -1
		Stop
	}
}

ACTOR AstroTurret : MGTurret
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Turret
	//$Color 4
	Scale 1.1
	Speed 0
	Obituary "$MGTURRET"
	Health 100
	Radius 15
	Height 21
	PainChance 100
	-FLOAT
	+DONTFALL
	+DONTTHRUST
	+MISSILEEVENMORE
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Spawn:
		ASTR A 0 NODELAY A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	Missile:
		"####" A 10 A_FaceTarget
	Missile.Aimed:
		"####" A 5 A_FaceTarget
		"####" B 0 A_PlaySound("astrochaingun/fire")
		"####" C 3 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",10,0,random(-8,8))
		"####" B 3 A_MonsterRefire(40,"See")
		"####" B 0 A_FaceTarget
		Goto Missile+1
	Death:
		TNT1 A 0 A_PlaySound("astrostein/explosion", CHAN_AUTO, 1.0, FALSE, 0.1)
		TNT1 A 0 A_SpawnItemEx("AstrosteinExplosion_Small")
		ASTR D -1
		Stop
	Crash:
		TNT1 A 0
		Stop
	Idle:
		Goto Look
	}
}

//////////
//BOSSES//
//////////

ACTOR AstroCyborg1 : NaziBoss
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Cyborg (1)
	//$Color 4
	Base.BossIcon "BOSSICON"
	Tag "Astro Cyborg"
	Health 500
	Radius 31
	Height 56
	Mass 400
	Speed 2.66666667
	Scale 0.67
	PainChance 32
	+BOSS
	+LOOKALLAROUND
	+NOBLOOD
	Obituary "$ACYBORG"
	SeeSound "astrostein/cyborg_sight"
	PainSound "astrostein/cyborg_pain"
	DeathSound "astrostein/cyborg_death"
	ActiveSound "astrostein/cyborg_act"
	DropItem "AstroRocketAmmo" 128
	States
	{
	Spawn:
		CYB1 J 0
		Goto Look
	See:
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
	Missile.Aimed:
		CYB1 G 10 A_FaceTarget
		"####" H 0 A_SpawnProjectile("AstroRocket",54,-32,random(-8,8))
		"####" H 2 A_SpawnProjectile("AstroRocket",54, 32,random(-8,8))
		Goto See
	Pain:
	Pain.Dagger:
		CYB1 I 3
		"####" I 9 A_Pain
		Goto See
	Death:
		CYB1 J 8 A_Scream
		"####" J 0 A_NoBlocking
		"####" "#" 0 A_SpawnItemEx("BaseLine", random(32, -32), random(32, -32), random(0, 24), 0, 0, random(1,3), 0, 129, 0)
		"####" "#" 1 A_FadeOut(0.05)
		Goto Death+2
	}
}

ACTOR AstroCyborg2 : AstroCyborg1
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Cyborg (2)
	//$Color 4
	DropItem "AstroShotgunShell" 256
	DropItem "AstroShotgunShell" 256
	States
	{
	Spawn:
		CYB2 H 0
		Goto Look
	See:
		CYB2 A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_NaziChase
		"####" AA 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_NaziChase
		"####" BB 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_NaziChase
		"####" CC 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_NaziChase
		"####" DD 1 A_NaziChase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
	Missile.Aimed:
		CYB2 E 10 A_FaceTarget
		"####" F 0 A_SpawnProjectile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" F 3 A_SpawnProjectile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" E 3 A_FaceTarget
		"####" F 0 A_SpawnProjectile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" F 3 A_SpawnProjectile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" E 3 A_FaceTarget
		"####" F 0 A_SpawnProjectile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" F 3 A_SpawnProjectile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" E 3 A_FaceTarget
		"####" F 0 A_SpawnProjectile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" F 3 A_SpawnProjectile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" E 3 A_FaceTarget
		"####" F 0 A_SpawnProjectile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" F 3 A_SpawnProjectile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" E 3 A_FaceTarget
		"####" F 0 A_SpawnProjectile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" F 3 A_SpawnProjectile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION|CMF_BADPITCH, random(-8,8))
		"####" E 3 A_FaceTarget
		Goto See
	Pain:
	Pain.Dagger:
		CYB2 G 3
		"####" G 9 A_Pain
		Goto See
	Death:
		CYB2 H 8 A_Scream
		"####" H 0 A_NoBlocking
		Goto Super::Death+2
	}
}

ACTOR AstroRobot : AstroCyborg1
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Robot
	//$Color 4
	Base.BossIcon "BOSSICON"
	Tag "Tiger Roboter"
	Health 750
	Scale 0.70
	+AVOIDMELEE
	+BOSS
	Obituary "$AROBOTO"
	SeeSound "astrostein/robot_sight"
	PainSound "astrostein/robot_pain"
	DeathSound "astrostein/robot_death"
	ActiveSound "astrostein/robot_act"
	DropItem "AstroRocketAmmo" 160
	States
	{
	Spawn:
		ROB2 A 0
		Goto Look
	Missile:
		ROB2 HG 5 A_FaceTarget
		"####" G 0 A_Jump(128, "Missile3")
		"####" G 0 A_Jump(128, 2)
		"####" G 0 A_Jump(128, "Missile2")
		"####" F 0 A_SpawnProjectile("AstroRobotSeekerBall",64,-36,random(-8,8))
		"####" F 5 A_SpawnProjectile("AstroRobotSeekerBall",64,36,random(-8,8))
		Goto See
	Missile2:
		"####" F 5 A_SpawnProjectile("AstroRobotSeekerBall",64,-36,random(-8,8))
		Goto See
	Missile3:
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 0 A_SpawnProjectile("EnemyAstroTracer",64,-36,random(-8,8))
		"####" F 8 LIGHT(ASTROFIRE) A_SpawnProjectile("EnemyAstroTracer",64,36,random(-8,8))
		Goto See
	Pain:
	Pain.Dagger:
	Missile.Aimed:
		ROB2 H 3
		"####" H 9 A_Pain
		Goto See
	Death.Fire: //We don't want different Death sequences than the Disintegrate one for Astrosteins, yeah?
	XDeath:
	Death:
	Disintegrate:
		ROB2 H 4 A_Scream
		"####" IJKLMNO 12
		"####" O 1 A_SpawnItemEx("AstroRobotDropper", 0, 0, 0)
		Stop
	}
}

ACTOR AstroHitler : NaziBoss
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Hitler Cyborg
	//$Color 4
	Base.BossIcon "BOSSICO3"
	Tag "Adolf Hitler"
	Health 1500
	Mass 1000
	Height 72
	Radius 36
	Scale 2.0
	PainChance 128
	Monster
	+BOSS
	+FLOORCLIP
	+NOBLOOD
	+DONTTHRUST
	Obituary "$AHITLER"
	ActiveSound "astrostein/hitler_act"
	DeathSound "astrostein/hitler_dth"
	SeeSound "astrostein/hitler_act"
	PainSound "astrostein/hitler_pai"
	States
	{
	Spawn:
		ADHI A 6 A_Look
		Loop
	See:
		ADHI ABC 6 A_Chase
		Loop
	Pain:
		ADHI E 3
		"####" E 3 A_Pain
		Goto See
	Death:
		ADHI E 35 A_Scream
		"####" E 0 A_PlaySound("astrostein/explosion", CHAN_AUTO, 1.0, FALSE, 0.1)
		"####" E 0 A_SpawnItemEx("GeneralExplosion_Medium")
		"####" E 25
		"####" E 0 A_PlaySound("astrostein/explosion", CHAN_AUTO, 1.0, FALSE, 0.1)
		"####" E 0 A_SpawnItemEx("GeneralExplosion_Medium")
		"####" E 15
		"####" E 0 A_PlaySound("astrostein/explosion", CHAN_AUTO, 1.0, FALSE, 0.1)
		"####" E 0 A_SpawnItemEx("GeneralExplosion_Large")
		"####" E 5
		"####" E 0 A_PlaySound("astrostein/explosion", CHAN_AUTO, 1.0, FALSE, 0.1)
		"####" E 0 A_SpawnItemEx("GeneralExplosion_Large")
		"####" FGHIJ 20
		"####" J -1
		Stop
	}
}