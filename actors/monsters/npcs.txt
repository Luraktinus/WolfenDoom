ACTOR StatistBarkeeper : Base
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Juliette Bertrand, Barkeeper (NPC)
	//$Color 4
	Height 56
	Mass 100
	Scale 0.65
	+SOLID
	+USESPECIAL
	States
	{
	Spawn:
		BDKP A 80
		"####" B 40
		"####" CB 40
		"####" D 80
		"####" DACB 40
		"####" E 40
		"####" F 40
		Loop
	}
}

ACTOR StatistMedic : Base
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Ty Halderman, Medic (NPC)
	//$Color 4
	Height 56
	Mass 100
	Scale 0.67
	+SOLID
	States
	{
	Spawn:
		MEDC A 80
		"####" B 40
		"####" CB 40
		"####" D 80
		"####" DACB 40
		"####" E 40
		"####" F 40
		Loop
	}
}

ACTOR StatistBarkeeperMale : StatistBarkeeper
{
	//$Title Randi Heit, Barkeeper (NPC)
	States
	{
	Spawn:
		BDK2 A 80
		"####" B 40
		"####" CB 40
		"####" D 80
		"####" DACB 40
		"####" E 40
		"####" F 40
		Loop
	}
}

ACTOR GeneralAllied : Base
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title General Miller
	//$Color 4
	Obituary "$MILLER"
	Height 56
	Mass 100
	Scale 0.67
	+SOLID
	States
	{
	Spawn:
		GENR O -1
		Stop
	}
}

ACTOR StatistSinger : SwitchableDecoration
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Marlene Dietrich (NPC)
	//$Color 4
	//$Sprite MADIA0
	Height 56
	Mass 100
	Scale 0.67
	+SOLID
	States
	{
	Spawn:
	Active:
		MADI ABCDEFBGDHFIB 8 A_SetSolid
		Loop
	Inactive:
		TNT1 A -1 A_UnSetSolid
		Loop
	}
}

ACTOR KittySinger
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Saloon Kitty (NPC, Shootable)
	//$Color 4
	Height 56
	Health 10
	Mass 600
	Scale 0.67
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	Species "Nazi"
	States
	{
	Spawn:
		KITY A 0 NoDelay
	SpawnLoop:
		"####" A 0 A_CheckRange(768,"SpawnLoopSilent")
		"####" A 1
		"####" B 0 A_PlaySound("SALOONKY", CHAN_AUTO, 1.0, TRUE, ATTN_STATIC)
		"####" ABCDBACDADCB 8
		Loop
	SpawnLoopSilent:
		"####" A 0 A_StopSound(CHAN_AUTO)
		"####" ABCDBACDADCB 8
		Goto SpawnLoop
	Death:
		"####" E 0 A_StopSound(CHAN_AUTO)
		"####" E 1 A_UnSetSolid
		"####" FGHIJ 5
		"####" K -1
		Stop
	}
}

ACTOR Marine3AStatist : Marine1
{
	Height 56
	Mass 100
	Scale 0.67
	+FRIENDLY
	+SOLID
	States
	{
	Spawn:
		ARH2 A -1
		Stop
	}
}

ACTOR Marine3BStatist : Marine3AStatist
{
	States
	{
	Spawn:
		MAH2 A -1
		Stop
	}
}

//Concentration Camp NPCS
ACTOR Camp_StaticBroom
{
	//$Category Concentration Camp (Wolf3D)/Static
	//$Title Shootable Prisoner (with broom)
	//$Color 3
	Height 48
	Health 10
	Mass 600
	Scale 0.65
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	DeathSound "Nazi1/Death" //this will need fitting sounds I guess
	States
	{
	Spawn:
		PRSA A -1
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" BCD 6
		"####" E 8 A_Scream
		"####" F -1
		Stop
	}
}

ACTOR Camp_StaticShovel : Camp_StaticBroom
{
	//$Title Shootable Prisoner (with shovel)
	States
	{
	Spawn:
		PRSB A -1
		Stop
	}
}

ACTOR Camp_StaticShovelLow : Camp_StaticBroom
{
	//$Title Shootable Prisoner (with shovel, low)
	Height 24
	+DONTTHRUST
	States
	{
	Spawn:
		PRSD A -1
		Stop
	}
}

ACTOR Camp_StaticKneeing : Camp_StaticBroom
{
	//$Title Shootable Prisoner (hurt, kneel)
	Height 32
	States
	{
	Spawn:
		PRSE A -1
		Stop
	}
}

ACTOR Camp_StaticStandingA : Camp_StaticBroom
{
	//$Title Shootable Prisoner (standing, bald)
	States
	{
	Spawn:
		PRSF A -1
		Stop
	}
}

ACTOR Camp_StaticStandingB : Camp_StaticBroom
{
	//$Title Shootable Prisoner (standing, hat)
	States
	{
	Spawn:
		PRSG A -1
		Stop
	}
}

ACTOR Camp_StaticWashBroom : Camp_StaticBroom
{
	//$Title Shootable Prisoner (washbroom)
	States
	{
	Spawn:
		PRSH A -1
		Stop
	}
}

ACTOR Camp_StaticSit : Camp_StaticBroom
{
	//$Title Shootable Prisoner (sit on a bench)
	Height 32
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		PRSI A -1
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" B 8 A_Scream
		"####" B -1
		Stop
	}
}

ACTOR Camp_StaticShovelDig
{
	//$Category Concentration Camp (Wolf3D)/Static
	//$Title Shootable Prisoner (digging with shovel)
	//$Color 3
	Height 48
	Health 10
	Mass 600
	Scale 0.65
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	DeathSound "Nazi1/Death" //this will need fitting sounds I guess
	States
	{
	Spawn:
		PRSC A 0 NoDelay
	SpawnLoop:
		PRSC A 0 A_CheckRange(512,"SpawnLoopSilent")
		PRSC A 1 A_SetTics(random(24,32))
		"####" B 0 A_PlaySound("floor/dirt", CHAN_ITEM, 1.0, FALSE)
		"####" BCDE 1 A_SetTics(random(12,16))
		Loop
	SpawnLoopSilent:
		PRSC A 0 A_StopSound(CHAN_ITEM)
		"####" A 1 A_SetTics(random(24,32))
		"####" BCDE 1 A_SetTics(random(12,16))
		Goto SpawnLoop
	Death:
		"####" A 0 A_UnSetSolid
		"####" FGH 8
		"####" I 6 A_Scream
		"####" J -1
		Stop
	}
}

ACTOR Camp_PrisonerBald : Nazi //it has shooting frames but actually he doesn't - ozy81
{
	//$Category Concentration Camp (Wolf3D)
	//$Title Wandering Prisoner (bald)
	//$Color 3
	Health 25
	PainChance 200
	Speed 1
	Radius 20
	Height 50
	Monster
	-COUNTKILL
	+FLOORCLIP
	+FRIENDLY
	+JUSTHIT
	+NEVERTARGET
	+NOSPLASHALERT
	MaxStepHeight 16
	MaxDropoffHeight 32
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	States
	{
	Spawn:
		PRSN E 0 NoDelay {
		A_ChangeFlag ("USESPECIAL", FALSE); //let's pretend when the actor is active we can't interact with it
		A_SetScale(ScaleX + frandom(-0.2,0.2),ScaleY + frandom(-0.2,0.2));
		}
	Active: //check if it's active though
	SpawnLoop:
		PRSN E 0 A_Look2
		Loop
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		"####" "#" 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" AA 1 A_Wander
		"####" AAAAA 1 A_Wander
		"####" AA 1 A_Wander
		"####" AAAAA 1 A_Wander
		"####" BB 1 A_Wander
		"####" BBBBB 1 A_Wander
		"####" BB 1 A_Wander
		"####" BBBBB 1 A_Wander
		"####" "#" 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" CC 1 A_Wander
		"####" CCCCC 1 A_Wander
		"####" CC 1 A_Wander
		"####" CCCCC 1 A_Wander
		"####" DD 1 A_Wander
		"####" DDDDD 1 A_Wander
		"####" DD 1 A_Wander
		"####" DDDDD 1 A_Wander
		"####" A 0 A_Jump(32, "LookAround")
		Loop
	LookAround:
		"####" E 10 A_SetAngle(random(-45,45)+angle)
		"####" E 0 A_SetTics(random(35,105))
		"####" E 0
		"####" A 0 A_Jump(32, "See")
		"####" A 0 A_Jump(8, "Cough") //insert from here random sounds and give each call a separate state - ozy81
		Loop
	Cough:
		"####" E 0 A_CheckRange(768,"NoCough")
		"####" E 80 A_PlaySound("camp/cough", CHAN_ITEM, 0.8, FALSE)
		"####" A 0 A_Jump(32, "See")
		Loop
	NoCough:
		"####" E 0 A_StopSound(CHAN_ITEM)
		"####" E 80
		"####" A 0 A_Jump(32, "See")
		Loop
	Pain:
		"####" H 6 A_NaziPain
		Goto SpawnLoop
	XDeath: //do we need the burn state here? could be this legit or it's just useless for the taste of people? you know what I mean
	Death:
		"####" I 5 A_Scream
		"####" J 6
		"####" K 5 A_NoBlocking
		"####" L 5 A_Jump(1,2,3)
		"####" M -1
		"####" N -1
		"####" O -1
		Stop
	Inactive:
		"####" E 0 A_ChangeFlag ("USESPECIAL", TRUE)
		"####" E -1
		Stop
	}
}