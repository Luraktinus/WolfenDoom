ACTOR StatistBarkeeper : Base
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Juliette Bertrand, Barkeeper (NPC)
	//$Color 4
	Height 56
	Mass 100
	Scale 0.65
	+SOLID
	+USESPECIAL
	Monster
	States
	{
	Spawn:
		BDKP A 80
		"####" B 40
		"####" CB 40
		"####" D 80
		"####" DACB 40
		"####" E 40
		"####" F 40
		Loop
	}
}

ACTOR StatistMedic : StatistBarkeeper
{
	//$Title Ty Halderman, Medic (NPC)
	Scale 0.67
	-USESPECIAL
	States
	{
	Spawn:
		MEDC A 80
		"####" B 40
		"####" CB 40
		"####" D 80
		"####" DACB 40
		"####" E 40
		"####" F 40
		Loop
	}
}

ACTOR StatistBarkeeperMale : StatistBarkeeper
{
	//$Title Randi Heit, Barkeeper (NPC)
	States
	{
	Spawn:
		BDK2 A 80
		"####" B 40
		"####" CB 40
		"####" D 80
		"####" DACB 40
		"####" E 40
		"####" F 40
		Loop
	}
}

ACTOR StatistSinger : SwitchableDecoration
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Marlene Dietrich (NPC)
	//$Color 4
	//$Sprite MADIA0
	Height 56
	Mass 100
	Scale 0.67
	+SOLID
	States
	{
	Spawn:
	Active:
		MADI ABCDEFBGDHFIB 8 A_SetSolid
		Loop
	Inactive:
		TNT1 A -1 A_UnSetSolid
		Loop
	}
}

ACTOR StatistDarren : StatistBarkeeper
{
	//$Title Dirty Douglas, Headquarters (NPC)
	Translation 1
	Species "Ally"
	+FRIENDLY
	+INVULNERABLE
	+NONSHOOTABLE
	States
	{
	Spawn:
		DARS A 0 NoDelay A_Jump(256,"Idle1","Idle2","Idle3","Idle4","Idle5","Idle6")
	Idle1:
		"####" A 20
		"####" B 40
		"####" CB 40
		"####" A 80
		"####" TACB 40
		"####" B 40
		"####" C 40
		"####" C 0 A_Jump(32,"Idle2","Idle3","Idle6")
		Goto Spawn
	Idle2:
		"####" A 20
		"####" D 40
		"####" EF 40
		"####" A 80
		"####" DFEA 40
		"####" F 40
		"####" D 40
		"####" C 0 A_Jump(32,"Idle1","Idle3","Idle6")
		Goto Spawn
	Idle3:
		"####" G 20
		"####" H 40
		"####" IH 40
		"####" G 80
		"####" HIGH 40
		"####" I 40
		"####" G 40
		"####" G 0 A_Jump(32,"Idle2","Idle4")
		Goto Spawn
	Idle4:
		"####" J 20
		"####" K 40
		"####" LK 40
		"####" J 80
		"####" KJLK 40
		"####" L 40
		"####" J 40
		"####" J 0 A_Jump(32,"Idle5","Idle6")
		Goto Spawn
	Idle5:
		"####" M 40
		"####" N 80
		"####" MO 80
		"####" G 80
		"####" MNON 40
		"####" M 40
		"####" O 60
		"####" O 0 A_Jump(32,"Idle2","Idle4")
		Goto Spawn
	Idle6:
		"####" P 80
		"####" QRSQ 40
		"####" P 40
		"####" T 40
		"####" T 0 A_Jump(32,"Idle1","Idle2","Idle4")
		Goto Spawn
	}
}

ACTOR KittySinger
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Saloon Kitty (NPC, Shootable)
	//$Color 4
	Height 56
	Health 10
	Mass 600
	Scale 0.67
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	Species "Nazi"
	States
	{
	Spawn:
		KITY A 0 NoDelay
	SpawnLoop:
		"####" A 0 A_CheckRange(768,"SpawnLoopSilent")
		"####" A 1
		"####" B 0 A_PlaySound("SALOONKY", CHAN_AUTO, 1.0, TRUE, ATTN_STATIC)
		"####" ABCDBACDADCB 8
		Loop
	SpawnLoopSilent:
		"####" A 0 A_StopSound(CHAN_AUTO)
		"####" ABCDBACDADCB 8
		Goto SpawnLoop
	Death:
		"####" E 0 A_StopSound(CHAN_AUTO)
		"####" E 1 A_UnSetSolid
		"####" FGHIJ 5
		"####" K -1
		Stop
	}
}

ACTOR Marine3AStatist : Marine1
{
	Height 56
	Mass 100
	Scale 0.67
	+FRIENDLY
	+SOLID
	States
	{
	Spawn:
		ARH2 A -1
		Stop
	}
}

ACTOR Marine3BStatist : Marine3AStatist
{
	States
	{
	Spawn:
		MAH2 A -1
		Stop
	}
}

ACTOR GeneralAllied : Base
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title General Miller
	//$Color 4
	Obituary "$MILLER"
	Height 56
	Mass 100
	Scale 0.67
	+SOLID
	States
	{
	Spawn:
		GENR O -1
		Stop
	}
}

//Concentration Camp NPCS
ACTOR Camp_StaticBroom
{
	//$Category Concentration Camp (Wolf3D)/Static
	//$Title Shootable Prisoner (with broom)
	//$Color 3
	Height 48
	Health 10
	Mass 600
	Scale 0.65
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	DeathSound "Nazi1/Death" //this will need fitting sounds I guess
	States
	{
	Spawn:
		PRSA A -1
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" BCD 6
		"####" E 8 A_Scream
		"####" F -1
		Stop
	}
}

ACTOR Camp_StaticShovel : Camp_StaticBroom
{
	//$Title Shootable Prisoner (with shovel)
	States
	{
	Spawn:
		PRSB A -1
		Stop
	}
}

ACTOR Camp_StaticShovelLow : Camp_StaticBroom
{
	//$Title Shootable Prisoner (with shovel, low)
	Height 24
	+DONTTHRUST
	States
	{
	Spawn:
		PRSD A -1
		Stop
	}
}

ACTOR Camp_StaticKneeing : Camp_StaticBroom
{
	//$Title Shootable Prisoner (hurt, kneel)
	Height 32
	States
	{
	Spawn:
		PRSE A -1
		Stop
	}
}

ACTOR Camp_StaticStandingA : Camp_StaticBroom
{
	//$Title Shootable Prisoner (standing, bald)
	States
	{
	Spawn:
		PRSF A -1
		Stop
	}
}

ACTOR Camp_StaticStandingB : Camp_StaticBroom
{
	//$Title Shootable Prisoner (standing, hat)
	States
	{
	Spawn:
		PRSG A -1
		Stop
	}
}

ACTOR Camp_StaticStandingBTalk : Camp_StaticStandingB
{
	//$Title Shootable Prisoner (standing, hat, talkable)
	-NOTAUTOAIMED	// Conversation fix - Talon1024
}

ACTOR Camp_StaticWashBroom : Camp_StaticBroom
{
	//$Title Shootable Prisoner (washbroom)
	States
	{
	Spawn:
		PRSH A -1
		Stop
	}
}

ACTOR Camp_StaticSit : Camp_StaticBroom
{
	//$Title Shootable Prisoner (sit on a bench)
	Height 32
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		PRSI A -1
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" B 8 A_Scream
		"####" B -1
		Stop
	}
}

ACTOR Camp_StaticShovelDig
{
	//$Category Concentration Camp (Wolf3D)/Static
	//$Title Shootable Prisoner (digging with shovel)
	//$Color 3
	Height 48
	Health 10
	Mass 600
	Scale 0.65
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	DeathSound "Nazi1/Death" //this will need fitting sounds I guess
	States
	{
	Spawn:
		PRSC A 0 NoDelay
	SpawnLoop:
		PRSC A 0 A_CheckRange(512,"SpawnLoopSilent")
		PRSC A 1 A_SetTics(random(24,32))
		"####" B 0 A_PlaySound("floor/dirt", CHAN_ITEM, 1.0, FALSE)
		"####" BCDE 1 A_SetTics(random(12,16))
		Loop
	SpawnLoopSilent:
		PRSC A 0 A_StopSound(CHAN_ITEM)
		"####" A 1 A_SetTics(random(24,32))
		"####" BCDE 1 A_SetTics(random(12,16))
		Goto SpawnLoop
	Death:
		"####" A 0 A_UnSetSolid
		"####" FGH 8
		"####" I 6 A_Scream
		"####" J -1
		Stop
	}
}

ACTOR Camp_PrisonerBald : NaziStandard //it has shooting frames but actually he doesn't - ozy81
{
	//$Category Concentration Camp (Wolf3D)
	//$Title Wandering Prisoner (bald)
	//$Color 3
	Health 25
	PainChance 255 //let's force it for Frightened - ozy81
	Speed 1
	Radius 20
	Height 50
	Mass 80
	Monster
	-COUNTKILL
	+FLOORCLIP
	+FRIENDLY
	+JUSTHIT
	+NEVERTARGET
	+NOSPLASHALERT
	+PUSHABLE
	+SLIDESONWALLS
	MaxStepHeight 16
	MaxDropoffHeight 32
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	States
	{
	Spawn:
		PRSN E 0 NoDelay {
		A_ChangeFlag ("USESPECIAL", FALSE); //let's pretend when the actor is active we can't interact with it
		A_SetScale(ScaleX + frandom(-0.2,0.2),ScaleY + frandom(-0.2,0.2));
		}
	Active: //check if it's active though
	SpawnLoop:
		PRSN E 1 {
			// Previously used A_Look2, but A_Look2 does NOT execute specials, so made it impossible to set navigation goals
			// This replicates the essential bits (alert on sound only, and jump to See after a random interval)
			A_LookEx(LOF_NOSIGHTCHECK);
			if (Random[wander](0, 255) < 30) { return resolveState("See"); }
			return resolveState("");
		}
		Loop
	See:
		"####" "#" 0 {A_SetUserVar("user_incombat", TRUE); A_SetSpeed(1);}
		"####" "#" 0 A_CheckFlag("FRIGHTENED", "See.Frightened", AAPTR_DEFAULT)
		"####" "#" 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" AA 1 A_WanderGoal(0, 768)
		"####" AAAAA 1 A_WanderGoal(0, 768)
		"####" AA 1 A_WanderGoal(0, 768)
		"####" AAAAA 1 A_WanderGoal(0, 768)
		"####" BB 1 A_WanderGoal(0, 768)
		"####" BBBBB 1 A_WanderGoal(0, 768)
		"####" BB 1 A_WanderGoal(0, 768)
		"####" BBBBB 1 A_WanderGoal(0, 768)
		"####" "#" 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" CC 1 A_WanderGoal(0, 768)
		"####" CCCCC 1 A_WanderGoal(0, 768)
		"####" CC 1 A_WanderGoal(0, 768)
		"####" CCCCC 1 A_WanderGoal(0, 768)
		"####" DD 1 A_WanderGoal(0, 768)
		"####" DDDDD 1 A_WanderGoal(0, 768)
		"####" DD 1 A_WanderGoal(0, 768)
		"####" DDDDD 1 A_WanderGoal(0, 768)
		"####" A 0 {
			if (!goal && Random[lookaround](0, 255) < 32) { return resolveState("LookAround"); }
			return resolveState("");
		}
		Loop
	See.Frightened:
		"####" "#" 0 { A_SetSpeed(random(4,6)); }
		"####" "#" 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" A 1 A_WanderGoal(0, 768)
		"####" AAA 1 A_WanderGoal(0, 768)
		"####" A 1 A_WanderGoal(0, 768)
		"####" AAA 1 A_WanderGoal(0, 768)
		"####" B 1 A_WanderGoal(0, 768)
		"####" BBB 1 A_WanderGoal(0, 768)
		"####" B 1 A_WanderGoal(0, 768)
		"####" BBB 1 A_WanderGoal(0, 768)
		"####" "#" 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" C 1 A_WanderGoal(0, 768)
		"####" CCC 1 A_WanderGoal(0, 768)
		"####" C 1 A_WanderGoal(0, 768)
		"####" CCC 1 A_WanderGoal(0, 768)
		"####" D 1 A_WanderGoal(0, 768)
		"####" DDD 1 A_WanderGoal(0, 768)
		"####" D 1 A_WanderGoal(0, 768)
		"####" DDD 1 A_WanderGoal(0, 768)
		"####" A 0 {
			if (!goal && Random[lookaround](0, 255) < 32) { return resolveState("LookAround.Frightened"); }
			return resolveState("");
		}
		Goto See // Go back to normal see so that they can become un-frightened once the frightener is out of range
	LookAround:
		"####" E 10 A_SetAngle(random(-45,45)+angle)
		"####" E 0 A_SetTics(random(35,105))
		"####" E 0
		"####" A 0 A_Jump(32, "See")
		"####" A 0 A_Jump(4, "Cough") //insert from here random sounds and give each call a separate state - ozy81
		Loop
	LookAround.Frightened:
		"####" P 1 A_SetTics(random(12,16))
		"####" QRQRSQP 1 A_SetTics(random(48,96))
		"####" E 1
		Goto See.Frightened
	Cough:
		"####" A 0 A_Jump(32, "See") //try to reduce coughing probabilities in a significantly way - ozy81
		"####" E 0 A_CheckRange(768,"NoCough")
		"####" E 0 A_PlaySound("camp/cough", CHAN_ITEM, 0.8, FALSE)
		"####" E 80
		"####" A 0 A_Jump(32, "See")
		Loop
	NoCough:
		"####" E 0 A_StopSound(CHAN_ITEM)
		"####" E 80
		"####" A 0 A_Jump(32, "See")
		Loop
	Pain:
		PRSN H 1 A_SpawnItemEx("Pain_Overlay", ScaleX+3, 0, Height-8, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_USEBLOODCOLOR)
		"####" H 5 {
				A_Pain();
				bFrightened = True;
			}
		Goto SpawnLoop
	Death:
		"####" I 5 A_Scream
		"####" J 6
		"####" K 5 A_NoBlocking
		"####" L 5 A_Jump(1,2,3)
		"####" M -1
		"####" N -1
		"####" O -1
		Stop
	Inactive:
		"####" E 0 A_ChangeFlag ("USESPECIAL", TRUE)
		"####" E -1
		Stop
	}
}

ACTOR Camp_PrisonerBaldShirtless : Camp_PrisonerBald
{
	//$Title Wandering Prisoner, Shirtless (bald)
	Health 15
	States
	{
	Spawn:
		PRSS E 0 NoDelay {
		A_ChangeFlag ("USESPECIAL", FALSE);
		A_SetScale(ScaleX + frandom(-0.2,0.2),ScaleY + frandom(-0.2,0.2));
		}
	Active:
	SpawnLoop:
		PRSS E 1 {
			A_LookEx(LOF_NOSIGHTCHECK);
			if (Random[wander](0, 255) < 30) { return resolveState("See"); }
			return resolveState("");
		}
		Loop
	}
}