ACTOR Meal : Health
{
	//$Category Health (Wolf3D)
	//$Title Meals (2 variants+5)
	//$Color 6
	Scale 0.5
	Inventory.Amount 5
	Inventory.PickupMessage "$MEAL"
	States
	{
	Spawn:
		MEAL A -1 NoDelay A_Jump(128,1)
		"####" B -1
		Stop
	}
}

ACTOR StackedMeal : Meal
{
	Radius 1
	Height 1
	+CANPASS
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"MealA","MealB")
		MealA:
		MEAL A 1
		Loop
		MealB:
		MEAL B 1
		Loop
	}
}

ACTOR Dogfood : Health
{
	//$Category Health (Wolf3D)
	//$Title Dog Food (+2)
	//$Color 6
	//$Sprite DOGFA0
	Scale 0.5
	Inventory.Amount 2
	Inventory.PickupMessage "$DOGFOOD"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6,7)
		DOGF A -1
		DOGF B -1
		DOGF C -1
		DOGF D -1
		DOGF E -1
		DOGF F -1
		DOGF G -1
		Stop
	}
}

ACTOR Medikit_Small : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (small+10)
	//$Color 6
	Scale 0.5
	Inventory.Amount 10
	Inventory.PickupMessage "$MEDSML"
	States
	{
	Spawn:
		MEDI A -1
		Stop
	}
}

ACTOR Medikit_Medium : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (medium+20)
	//$Color 6
	Scale 0.5
	Inventory.Amount 20
	Inventory.PickupMessage "$MEDMID"
	States
	{
	Spawn:
		MEDI B -1
		Stop
	}
}

ACTOR Medikit_Large : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (large+40)
	//$Color 6
	Inventory.Amount 40
	Inventory.PickupMessage "$MEDBIG"
	States
	{
	Spawn:
		MEDI C -1
		Stop
	}
}

ACTOR Medikit_Fullhealth : Health //needed by Halderman on HQs
{
	Inventory.Amount 200
	Inventory.PickupMessage "$MEDBIG"
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

// Re-written from scratch to limit the amount the player can buy from the shop, and to not completely restore the player's health. - Talon1024
ACTOR VitalitySerum : CustomInventory
{
	//$Category Health (Wolf3D)
	//$Title Vitality Serum (max+5)
	//$Color 6
	Radius 20
	Scale 0.45
	Inventory.MaxAmount 10
	Inventory.PickupMessage "$SERUM"
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNTOSSABLE
	+COUNTITEM
	States
	{
		Spawn:
			VSRM ABC 6
			Loop
		Pickup:
			VSRM A 0
			{
				if (CountInv("VitalitySerum") < 10) {
					ACS_NamedExecuteAlways("VSerum Max Health Boost");
				}
			}
			Stop
		Use:
			// Does nothing except prevent the item from disappearing from the player's inventory.
			VSRM A 0
			Fail
	}
}

ACTOR FieldKit : HealthPickup
{
	//$Category Health (Wolf3D)
	//$Title Field Kit (Item, +25)
	//$Color 6
	Health 25
	Scale 0.50
	HealthPickup.AutoUse 1
	Tag "$TAGFELDK"
	Inventory.Icon "FKITC0"
	Inventory.InterHubAmount 15
	Inventory.MaxAmount 15
	Inventory.PickupMessage "$FKIT"
	Inventory.PickupSound "Misc/I_PkUp"
	Inventory.UseSound "Misc/I_PkUp"
	+COUNTITEM
	States
	{
	Spawn:
		FKIT AB 10
		Loop
	}
}

ACTOR BandagesPowerup : PowerRegeneration
{
	Powerup.Duration -10
	Powerup.Strength 1
}

ACTOR Bandages : PowerupGiver
{
	//$Category Health (Wolf3D)
	//$Title Bandages (+10)
	//$Color 6
	Scale 0.5
	Tag "$TAGBAIDS"
	Inventory.Icon "BAIDB0"
	Inventory.MaxAmount 5
	Inventory.PickupMessage "$BAID"
	Powerup.Type "BandagesPowerup"
	+COUNTITEM
	States
	{
	Spawn:
		BAID A -1
		Stop
	}
}