ACTOR Shit
{
	//$Category Props (Wolf3D)/Bath
	//$Title Shitpile (squeezable)
	//$Color 3
	Radius 2
	Height 4
	Health 1
	Scale 0.35
	+NOBLOOD
	+NODROPOFF
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+TOUCHY
	States
	{
	Spawn:
		SHIT A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("SHITHPNS", CHAN_ITEM, frandom(0.5,0.7), FALSE, ATTN_NORM)
		"####" A -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY / 2)
		Stop
	}
}

ACTOR BucketMop
{
	//$Category Props (Wolf3D)/Bath
	//$Title Mop with Bucket (destroyable)
	//$Color 3
	Radius 8
	Height 32
	Health 10
	Scale 0.45
	+DONTTHRUST
	+NOBLOOD
	+NODROPOFF
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		MOPP A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom(0.2,0.5), FALSE, ATTN_NORM)
		"####" A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom(0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		MOPP B -1
		Stop
	}
}

//3D ACTORS//
ACTOR ToiletShootable
{
	//$Category Props (Wolf3D)/Bath
	//$Title Toilet (shootable)
	//$Color 3
	DistanceCheck "scenelod"
	Health 1
	Radius 10
	Height 42
	Mass 999999
	+DONTGIB
	+NOBLOOD
	+NOICEDEATH
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	DeathSound "toilet/death"
	States
	{
	Spawn:
		MDLA A -1
		Stop
	Death:
		MDLA A 2 A_Scream
		"####" AAAAAA 0 A_SpawnItemEx("Debris_Bin", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
	Broken:
		MDLA B 0 A_CheckRange(512,"BrokenSilent")
		"####" B 0 A_PlaySound("toilet/broken", CHAN_AUTO, 1.0, TRUE, ATTN_STATIC)
		"####" B 1 A_SpawnItemEx("WaterSmokePuffSmall", 0, 0, 1, (0.1)*random(0, 4), 0, (0.1)*random(8, 16), random(0, 360), 128)
		Loop
	BrokenSilent:
		MDLA B 0 A_StopSound(CHAN_AUTO)
		"####" B 1 A_SpawnItemEx("WaterSmokePuffSmall", 0, 0, 1, (0.1)*random(0, 4), 0, (0.1)*random(8, 16), random(0, 360), 128)
		Goto Broken
	}
}

ACTOR ToiletTalking : ToiletShootable
{
	//$Category Conversation Stuff (Wolf3D)
	//$Title Talking Toilet (shootable)
	//$Color 5
	-NOTAUTOAIMED //or it will become impossible to interact with it
	var int user_angle;
	States
	{
	Spawn:
		MDLA A 1 {
			if (!user_angle) { user_angle = angle; }

			angle = user_angle;
		}
		Loop
	}
}

ACTOR WashingDish
{
	//$Category Props (Wolf3D)/Bath
	//$Title Washing Dish
	//$Color 3
	DistanceCheck "scenelod"
	Radius 8
	Height 32
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}