ACTOR SSUniformStatic
{
	//$Category Props (Wolf3D)/Bureau
	//$Title SS Uniform
	//$Color 3
	Radius 8
	Height 20
	Scale 0.67
	ProjectilePassHeight 4
	+SOLID
	States
	{
	Spawn:
		SSUN A -1
		Stop
	}
}

ACTOR UniformStatic : SSUniformStatic
{
	//$Title Uniform
	States
	{
	Spawn:
		SSUN B -1
		Stop
	}
}

ACTOR ScientistUniformStatic : SSUniformStatic
{
	//$Title Scientist Uniform
	Height 40
	Scale 0.7
	States
	{
	Spawn:
		SCUN A -1
		Stop
	}
}

ACTOR FoxUniformStatic : SSUniformStatic
{
	//$Title Desert Fox Uniform
	States
	{
	Spawn:
		SSUN C -1
		Stop
	}
}

ACTOR HitlerUniformStatic : SSUniformStatic
{
	//$Title Hitler's Downfall Uniform
	States
	{
	Spawn:
		DWNF A -1
		Stop
	}
}

ACTOR CoatHanger : SSUniformStatic
{
	//$Title Coat Hanger
	Height 40
	Scale 0.8
	States
	{
	Spawn:
		CTHN A -1
		Stop
	}
}

ACTOR PaperBox1
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Box (single)
	//$Color 3
	Radius 8
	Height 16
	+SOLID
	States
	{
	Spawn:
		BOX1 A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR PaperBox2 : PaperBox1
{
	//$Title Box (pack)
	Scale 0.5
	States
	{
	Spawn:
		BOX1 B -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR Mike : PaperBox1
{
	//$Title Microphone for Tables
	Radius 2
	Height 8
	Scale 0.3
	-SOLID
	States
	{
	Spawn:
		MIKE A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR BinSilver
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Recycle Bin (silver)
	//$Color 3
	Radius 16
	Height 32
	Health 5
	Mass 100
	-DROPOFF
	+NOBLOOD
	+NOTAUTOAIMED
	+PUSHABLE
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		BINS A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAA 0 A_SpawnItemEx("Debris_Bin", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 2 A_SpawnItemEx("MetalFrags")
		BINS B -1
		Stop
	}
}

ACTOR Plant1 : Tiltable
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Plant 1 (breakable, small)
	//$Color 3
	Radius 8
	Height 32
	Health 10
	Scale 0.5
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		PLNT A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Leaf", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 2 A_SpawnItemEx("BarrelFrags")
		PLNT C -1
		Stop
	}
}

ACTOR Plant2 : Plant1
{
	//$Title Plant 2 (breakable, small)
	States
	{
	Spawn:
		PLNT B -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Leaf", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 2 A_SpawnItemEx("BarrelFrags")
		PLNT D -1
		Stop
	}
}

ACTOR Plant3 : Plant1
{
	//$Title Plant 3 (breakable, small)
	Height 24
	Scale 1.0
	States
	{
	Spawn:
		DJPT A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Leaf", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 AAA 0 A_SpawnItemEx("Debris_Bin", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 2 A_SpawnItemEx("MetalFrags")
		Stop
	}
}

ACTOR Plant4 : Plant3
{
	//$Title Plant 4 (breakable, small)
	States
	{
	Spawn:
		DJPT B -1
		Stop
	}
}

ACTOR Plant5 : Plant1
{
	//$Title Plant 5 (breakable, detailed, tall)
	Scale 1.0
	States
	{
	Spawn:
		DJPT C -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Leaf", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 2 A_SpawnItemEx("BarrelFrags")
		DJPT G -1
		Stop
	}
}

ACTOR Plant6 : Plant1
{
	//$Title Plant 6 (breakable, detailed, short)
	Height 24
	Scale 1.0
	States
	{
	Spawn:
		DJPT D -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Leaf", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 2 A_SpawnItemEx("BarrelFrags")
		DJPT H -1
		Stop
	}
}

ACTOR Cup1 : Tiltable
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Cup (coffee)
	//$Color 3
	Radius 2
	Height 4
	Health 1
	Scale 0.15
	+CANPASS
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		CUP1 A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		TNT1 A 0 A_PlaySound("GLASS5")
		TNT1 AAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR Cup2 : Cup1
{
	//$Title Cups (empty)
	Scale 0.7
	States
	{
	Spawn:
		CUP3 A -1
		Stop
	}
}

ACTOR DecoVase : Cup1
{
	//$Title Decorative Vase (breakable)
	Scale 1.0
	DeathSound "Pot/Break"
	States
	{
	Spawn:
		DJPT E -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		TNT1 A 0 A_Scream
		TNT1 AAAA 0 A_SpawnItemEx("Debris_LeafY", random(0,4), random(0,8), random(0,24), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR SnookerBall : Tiltable
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Snooker Ball (random, mostly red)
	//$Color 3
	Scale 0.1
	Radius 4
	Height 4
	Mass 10
	+CANPASS
	+NOBLOOD
	+NOTAUTOAIMED
	+PUSHABLE
	+SHOOTABLE
	+SLIDESONWALLS
	+SOLID
	States
	{
	Spawn:
		SNKR A -1 NoDelay A_Jump(128,1)
		"####" B -1 A_Jump(128,1)
		"####" C -1 A_Jump(128,1)
		"####" D -1 A_Jump(32,1) //white one very rare
		"####" E -1 
		Stop
	}
}

ACTOR CasinoChips_Green //the only issue there is that we'll see some chips falling even with the single one... np anyawy - ozy
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Casino Chips (green, random)
	//$Color 3
	Radius 4
	Height 8
	Health 1
	Scale 0.75
	+CANPASS
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+TOUCHY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		CHP1 A -1 A_Jump(128,1)
		"####" B -1 A_Jump(128,1)
		"####" C -1 A_Jump(128,1)
		"####" D -1 A_Jump(128,1)
		"####" E -1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("CHIP6")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_ChipsGreen", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_UnsetSolid
		Stop
	}
}

ACTOR CasinoChips_Blue : CasinoChips_Green
{
	//$Title Casino Chips (red, random)
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		CHP2 A -1 A_Jump(128,1)
		"####" B -1 A_Jump(128,1)
		"####" C -1 A_Jump(128,1)
		"####" D -1 A_Jump(128,1)
		"####" E -1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("CHIP6")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_ChipsBlue", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_UnsetSolid
		Stop
	}
}

ACTOR CasinoChips_Red : CasinoChips_Green
{
	//$Title Casino Chips (blue, random)
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		CHP3 A -1 A_Jump(128,1)
		"####" B -1 A_Jump(128,1)
		"####" C -1 A_Jump(128,1)
		"####" D -1 A_Jump(128,1)
		"####" E -1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("CHIP6")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_ChipsRed", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_UnsetSolid
		Stop
	}
}

ACTOR Dollar
{
	Radius 2
	Height 2
	Damage 0
	Scale 0.1
	Projectile
	-NOGRAVITY
	+ROLLSPRITE
	States
	{
	Spawn:
		MNEY A 0 NoDelay A_SetGravity(frandom(0.0525,0.0725))
		SetSpawn:
		"####" A 0 A_Jump(256,1,2,3)
		"####" ABC 0 A_Jump(256,"SpawnLoop")
		SpawnLoop:
		"####" "#" 0 {A_SetTics(random(1,3)); A_SetRoll(roll+10.5, SPF_INTERPOLATE,0);}
		"####" "#" 0 {A_SetTics(random(1,3)); A_SetRoll(roll-10.5, SPF_INTERPOLATE,0);}
		Loop
	Death:
		"####" A 0 A_Jump(256,1,2,3)
		"####" ABC 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 0 A_ChangeFlag("ROLLSPRITE", FALSE)
		"####" "#" 1 A_SetTics(35*CallACS("DebrisLifetime"))
		Death1Wait:
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR MoneyBags : Plant1
{
	//$Title Paper Money Bags (breakable)
	Height 16
	States
	{
	Spawn:
		MOBG A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.02))
		SetSpawn:
		"####" A 0 A_Jump(256,1,2,3,4)
		"####" ABCD 0 A_Jump(256,"CompleteSpawn")
		CompleteSpawn:
		"####" "#" -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("GRASBRKS", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("Dollar", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("Dollar", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 AAAAAA 0 A_SpawnItemEx("Dollar", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 1 A_SpawnItemEx("BarrelFrags")
		Stop
	}
}

ACTOR Toys_Hans : Cup1
{
	//$Title Exhibit Toy, Hans Grosse (breakable)
	DeathSound "WOODBRK"
	Scale 0.25
	States
	{
	Spawn:
		TOYZ A -1
		Stop
	Death:
		TNT1 A 0 A_Scream
		"####" AAAA 0 A_SpawnItemEx("Debris_ToyHans", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAA 0 A_SpawnItemEx("Debris_Wood", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR Toys_Waffen : Toys_Hans
{
	//$Title Exhibit Toy, Waffen SS (breakable)
	States
	{
	Spawn:
		TOYZ B -1
		Stop
	Death:
		TNT1 A 0 A_Scream
		"####" AAAA 0 A_SpawnItemEx("Debris_ToyWaff", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAA 0 A_SpawnItemEx("Debris_Wood", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR CoffeeMachine : SwitchableDecoration
{
	//$Category Props (Wolf3D)/Bureau
	//$Title Coffee Machine (interactive, destroyable)
	//$Color 3
	Radius 4
	Height 16
	Health 5
	+DONTSPLASH
	+DONTTHRUST
	+LOOKALLAROUND //this is needed otherwise we can drink only while in front of it - ozy81
	+NOBLOOD
	+NOBLOODDECALS
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+USESPECIAL
	Activation THINGSPEC_Switch | THINGSPEC_ThingTargets
	States
	{
	Active:
		CUP2 A 0 A_JumpIfHealthLower(25, 1, AAPTR_TARGET)
		Goto SpawnSet
		"####" A 0 A_GiveInventory("CoffeeCounter", 1)
		"####" A 0 A_GiveToTarget("Health", 1)
		"####" A 0 A_PlaySound("misc/drink", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_JumpIfInventory("CoffeeCounter", 5, "Empty")
		Goto SpawnSet
	Spawn:
		CUP2 A 0 NoDelay A_SpawnItemEx("InteractionIcon25Health", ScaleX*4, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		SpawnSet:
		"####" A -1
		Stop
	Inactive:
		"####" A 0 A_StopSound(CHAN_AUTO)
		Goto SpawnSet
	Empty:
		"####" A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		"####" A 0 A_ChangeFlag("USESPECIAL", FALSE)
		Goto SpawnSet
	Death:
		TNT1 A 0 A_PlaySound("GLSBRK01", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		"####" A 0 A_ChangeFlag("USESPECIAL", FALSE)
		"####" A 0 A_UnSetSolid
		"####" AAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Medium", random(0,4), random(0,4), random(0,24), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		CUP2 B -1
		Stop
	}
}

//3D ACTORS//
ACTOR WaterMachine : CoffeeMachine
{
	//$Title Water Machine (interactive, destroyable)
	DistanceCheck "scenelod"
	Renderstyle "Translucent"
	Alpha 0.5
	States
	{
	Active:
		MDLA A 0 A_JumpIfHealthLower(25, 1, AAPTR_TARGET)
		Goto SpawnSet
		"####" A 0 A_GiveInventory("WaterCounter", 1)
		"####" A 0 A_GiveToTarget("Health", 1)
		"####" A 0 A_PlaySound("misc/drink", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_JumpIfInventory("WaterCounter", 5, "Empty")
		Goto SpawnSet
	Spawn:
		MDLA A 0 NoDelay {
			A_SpawnItemEx("InteractionIcon25Health", ScaleX*4, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
			A_SpawnItemEx("WaterMachineWater", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE);
		}
		SpawnSet:
		"####" A -1
		Stop
	Inactive:
		"####" A 0 A_StopSound(CHAN_AUTO)
		Goto SpawnSet
	Empty:
		"####" A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		"####" A 0 A_ChangeFlag("USESPECIAL", FALSE)
		Goto SpawnSet
	Death:
		TNT1 A 0 A_PlaySound("GLSBRK01", CHAN_AUTO, 1.0, FALSE, ATTN_NORM)
		"####" A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		"####" A 0 A_ChangeFlag("USESPECIAL", FALSE)
		"####" A 0 A_UnSetSolid
		"####" AAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Medium", random(0,4), random(0,4), random(0,24), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA B -1 A_SpawnItemEx("WaterMachineBrokenWater", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR WaterMachineWater
{
	DistanceCheck "scenelod"
	Renderstyle "Translucent"
	Alpha 0.2
	States
	{
		Spawn:
			MDLA A -1
			Stop
	}
}

ACTOR WaterMachineBrokenWater : WaterMachineWater {}
ACTOR CoffeeCounter : Inventory {Inventory.MaxAmount 5}
ACTOR WaterCounter : Inventory {Inventory.MaxAmount 5}

ACTOR BarSeat : BinSilver
{
	//$Title Bar Chair (Destroyable)
	DistanceCheck "scenelod"
	Radius 12
	Height 24
	States
	{
	Spawn:
		MDLA A -1
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("BarrelFrags")
		"####" B -1
		Stop
	}
}

ACTOR ChairFuture : BarSeat
{
	//$Category Props (Wolf3D)/Astrostein
	//$Title Bar Chair (Modern, Destroyable)
	DistanceCheck "scenelod"
	Radius 8
	Height 16
	States
	{
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METLBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		"####" B -1
		Stop
	}
}

ACTOR Chair1 : Plant1
{
	//$Title Chair (plain, wood)
	DistanceCheck "scenelod"
	Height 20
	Mass 999999
	Scale 1.0
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(10,30), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(20,40), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA BCDEFG 1
		MDLA H -1
		Stop
	}
}

ACTOR Chair2 : Chair1
{
	//$Title Chair (office, wood & metal)
	DistanceCheck "scenelod"
	States
	{
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 {A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM);
							A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM);}
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(10,40), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Trash2", random(10,40), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA BCDEFGHIJKLMNO 1
		MDLA P -1
		Stop
	}
}

ACTOR Chair3 : Chair1
{
	//$Title Chair, Detailed (office, wood)
	DistanceCheck "scenelod"
	States
	{
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(10,30), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(20,40), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA BCDEFGHIJKLMNO 1
		MDLA P -1
		Stop
	}
}

ACTOR Chair4 : Chair1
{
	//$Title Chair (plain, metal)
	DistanceCheck "scenelod"
	States
	{
	Death:
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" AAAAAAAAAA 0 A_SpawnItemEx("Debris_Trash2", random(10,40), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		MDLA BCDEFG 1
		MDLA H -1
		Stop
	}
}