ACTOR PotKitchen1 : Tiltable
{
	//$Category Props (Wolf3D)/Kitchen
	//$Title Sauce Pan (rusty, open)
	//$Color 3
	Radius 16
	Height 32
	Health 1
	Scale 0.5
	+CANPASS
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	States
	{
	Spawn:
		TOPF A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		TOPF B -1
		Stop
	}
}

ACTOR PotKitchen2 : PotKitchen1
{
	//$Title Frying Pan (cooking)
	Height 16
	States
	{
	Spawn:
		FCAN A 0 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
	Cook:
		FCAN ABC 5
		Loop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		FCAN D -1
		Stop
	}
}

ACTOR PotKitchen3 : PotKitchen1
{
	//$Title Frying Pan (empty)
	Height 8
	States
	{
	Spawn:
		ECAN A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		ECAN B -1
		Stop
	}
}

ACTOR PotKitchen4 : PotKitchen1
{
	//$Title Sauce Pan (rusty, closed)
	States
	{
	Spawn:
		TOPF C -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		TOPF D -1
		Stop
	}
}

ACTOR Kettle : PotKitchen1
{
	//$Title Kettle
	Radius 8
	Height 16
	States
	{
	Spawn:
		KETL A -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" A 2 A_SpawnItemEx("MetalFrags")
		KETL B -1
		Stop
	}
}

ACTOR WineBottle : PotKitchen1
{
	//$Title Bottle (liquor, random)
	Radius 4
	Height 8
	Scale 0.12
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7)
		FLAS A -1
		FLAS B -1
		FLAS C -1
		FLAS D -1
		FLAS E -1
		FLAS F -1
		FLAS G -1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("GLASS5")
		"####" AAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_UnsetSolid
		Stop
	}
}

ACTOR WineGlass : WineBottle
{
	//$Title Glass (glass, random)
	Radius 2
	Scale 0.05
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2)
		FLAS Y -1
		FLAS Z -1
		Stop
	}
}

ACTOR OvenHot : SwitchableDecoration
{
	//$Category Props (Wolf3D)/Kitchen
	//$Title Oven (Destroyable)
	//$Color 3
	Radius 16
	Height 64
	Health 35
	Scale 0.50
	+DONTTHRUST
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	+USESPECIAL
	Activation THINGSPEC_Switch //no need to update the DORMANT flag and the SWITCH property because we need it destroyable - ozy
	States
	{
	Active:
	Spawn:
		OVEN A 0 NoDelay A_PlaySound("SFX/FireLoop1", CHAN_BODY, 0.7, TRUE, ATTN_STATIC)
		Goto Frame1
	Frame1:
		OVEN A 0 A_CheckRange(768,"NoSound")
		OVEN A 4 A_Jump(64, "Frame3")
		Goto Frame2
	Frame2:
		OVEN B 0 A_CheckRange(768,"NoSound")
		OVEN B 4 A_Jump(64, "Frame4")
		Goto Frame3
	Frame3:
		OVEN C 0 A_CheckRange(768,"NoSound")
		OVEN C 4 A_Jump(64, "Frame1")
		Goto Frame4
	Frame4:
		OVEN D 0 A_CheckRange(768,"NoSound")
		OVEN D 4 A_Jump(64, "Frame2")
		Goto Frame1
	NoSound:
		TNT1 A 0 A_StopSound(CHAN_BODY)
	NoFrame1:
		OVEN A 4 A_Jump(64, "NoFrame3")
		Goto Spawn
	NoFrame2:
		OVEN B 4 A_Jump(64, "NoFrame4")
		Goto Spawn
	NoFrame3:
		OVEN C 4 A_Jump(64, "NoFrame1")
		Goto Spawn
	NoFrame4:
		OVEN D 4 A_Jump(64, "NoFrame2")
		Goto Spawn
	Inactive:
		OVEN E -1 A_StopSound(CHAN_BODY)
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_ChangeFlag("USESPECIAL", FALSE)
		"####" A 0 A_StopSound(CHAN_BODY)
		"####" A 0 A_PlaySound("METALBRK")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE) //make sure to always adjust XYZ position depending on the size of the object and the amount of AAA
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		"####" AAAAAA 0 A_SpawnItemEx("BarrelFireSpawner", random(8,12), random(8,12), random(0,56), 0, 0, 0, random(0,360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		OVEN F -1
		Stop
	}
}